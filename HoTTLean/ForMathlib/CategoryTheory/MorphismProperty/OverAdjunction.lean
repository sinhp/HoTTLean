/-
Copyright (c) 2024 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
import Mathlib.CategoryTheory.MorphismProperty.Comma
import Mathlib.CategoryTheory.Comma.Over.Pullback
import Mathlib.CategoryTheory.MorphismProperty.Limits
import HoTTLean.ForMathlib.CategoryTheory.MorphismProperty.Limits

/-!
# Adjunction of pushforward and pullback in `P.Over Q X`

Under suitable assumptions on `P`, `Q` and `f`,
a morphism `f : X ‚ü∂ Y` defines two functors:

- `Over.map`: post-composition with `f`
- `Over.pullback`: base-change along `f`

such that `Over.map` is the left adjoint to `Over.pullback`.
We say that `P` is *stable* under pushforward if `Over.pullback`
also is a left adjoint.
We say that `P` is *closed* under pushforward if `Over.pullback`
also is a left adjoint for any `f` satisfying `P`.

-/

namespace CategoryTheory.MorphismProperty

open Limits

variable {T : Type*} [Category T] (P Q : MorphismProperty T) [Q.IsMultiplicative]
variable {X Y Z : T} (f : X ‚ü∂ Y)

section Map

variable {P} [P.IsStableUnderComposition] (hPf : P f)

variable {f}

/-- If `P` is stable under composition and `f : X ‚ü∂ Y` satisfies `P`,
this is the functor `P.Over Q X ‚•§ P.Over Q Y` given by composing with `f`. -/
@[simps! obj_left obj_hom map_left]
def Over.map : P.Over Q X ‚•§ P.Over Q Y :=
  Comma.mapRight _ (Discrete.natTrans fun _ ‚Ü¶ f) <| fun X ‚Ü¶ P.comp_mem _ _ X.prop hPf

lemma Over.map_comp {X Y Z : T} {f : X ‚ü∂ Y} (hf : P f) {g : Y ‚ü∂ Z} (hg : P g) :
    map Q (P.comp_mem f g hf hg) = map Q hf ‚ãô map Q hg := by
  fapply Functor.ext
  ¬∑ simp [map, Comma.mapRight, CategoryTheory.Comma.mapRight, Comma.lift]
  ¬∑ intro U V k
    ext
    simp

/-- `Over.map` commutes with composition. -/
@[simps! hom_app_left inv_app_left]
def Over.mapComp {X Y Z : T} {f : X ‚ü∂ Y} (hf : P f) {g : Y ‚ü∂ Z} (hg : P g) [Q.RespectsIso] :
    map Q (P.comp_mem f g hf hg) ‚âÖ map Q hf ‚ãô map Q hg :=
  NatIso.ofComponents (fun X ‚Ü¶ Over.isoMk (Iso.refl _))

end Map

section Pullback

variable [P.HasPullback f] [P.IsStableUnderBaseChange] [Q.IsStableUnderBaseChange]

variable {P Q} in
@[simps]
def Over.morphismProperty (f : P.Over Q X) : f.left ‚ü∂(P) X := ‚ü® f.hom , f.prop ‚ü©

instance (A : P.Over Q Y) : HasPullback A.hom f :=
  inferInstanceAs (HasPullback (A.morphismProperty).1 f)

/-- If `P` and `Q` are stable under base change and pullbacks exist in `T`,
this is the functor `P.Over Q Y ‚•§ P.Over Q X` given by base change along `f`. -/
@[simps! obj_left obj_hom map_left]
noncomputable def Over.pullback : P.Over Q Y ‚•§ P.Over Q X where
  obj A := Over.mk Q (Limits.pullback.snd A.morphismProperty.1 f)
    (baseChange_obj f A.toComma A.prop)
  map {A B} g := Over.homMk (pullback.map _ f _ f g.left (ùüô _) (ùüô _) (by simp) (by simp))
    (by simp) (baseChange_map f ‚ü®g.left, g.right, _‚ü© g.prop_hom_left)

variable {P} {Q}

instance [P.IsStableUnderBaseChange] {X Y Z}
    (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) [P.HasPullback f] [P.HasPullback g] (A : P.Over Q Z) :
    HasPullback (pullback.snd A.hom g) f :=
  inferInstanceAs <| HasPullback (pullback.snd A.morphismProperty.1 g) f

lemma Over.hom_pullback_map [‚àÄ {W : T} (h : W ‚ü∂ Y), HasPullback h f] {A B} (g : A ‚ü∂ B) :
    Comma.Hom.hom ((Over.pullback P Q f).map g) =
    (CategoryTheory.Over.pullback f).map (Comma.Hom.hom g) := by
  simp [Over.pullback, CategoryTheory.Over.pullback, pullback.map]

/-- `Over.pullback` commutes with composition. -/
@[simps! hom_app_left inv_app_left]
noncomputable def Over.pullbackComp (g : Y ‚ü∂ Z) [P.HasPullback g]
    [Q.RespectsIso] : Over.pullback P Q (f ‚â´ g) ‚âÖ Over.pullback P Q g ‚ãô Over.pullback P Q f :=
  NatIso.ofComponents
    (fun X ‚Ü¶
      Over.isoMk ((pullbackLeftPullbackSndIso X.hom g f).symm) (by simp))

lemma Over.pullbackComp_left_fst_fst (g : Y ‚ü∂ Z) [P.HasPullback g]
    [Q.RespectsIso] (A : P.Over Q Z) : ((Over.pullbackComp f g).hom.app A).left ‚â´
      pullback.fst (pullback.snd A.hom g) f ‚â´ pullback.fst A.hom g =
        pullback.fst A.hom (f ‚â´ g) := by
  simp

variable {f}
/-- If `f = g`, then base change along `f` is naturally isomorphic to base change along `g`. -/
noncomputable def Over.pullbackCongr {g : X ‚ü∂ Y} (h : f = g) :
    have : P.HasPullback g := by subst h; infer_instance
    Over.pullback P Q f ‚âÖ Over.pullback P Q g :=
  NatIso.ofComponents (fun X ‚Ü¶ eqToIso (by simp [h]))

@[reassoc (attr := simp)]
lemma Over.pullbackCongr_hom_app_left_fst {g : X ‚ü∂ Y} (h : f = g) (A : P.Over Q Y) :
    have : P.HasPullback g := by subst h; infer_instance
    ((Over.pullbackCongr h).hom.app A).left ‚â´ pullback.fst A.hom g =
      pullback.fst A.hom f := by
  subst h
  simp [pullbackCongr]

end Pullback

section Adjunction

variable [P.IsStableUnderComposition] [P.IsStableUnderBaseChange]
  [Q.IsStableUnderBaseChange] [P.HasPullback f]

/-- `P.Over.map` is left adjoint to `P.Over.pullback` if `f` satisfies `P`. -/
noncomputable def Over.mapPullbackAdj [Q.HasOfPostcompProperty Q] (hPf : P f) (hQf : Q f) :
    Over.map Q hPf ‚ä£ Over.pullback P Q f :=
  Adjunction.mkOfHomEquiv
    { homEquiv := fun A B ‚Ü¶
        { toFun := fun g ‚Ü¶
            Over.homMk (pullback.lift g.left A.hom <| by simp) (by simp) <| by
              apply Q.of_postcomp (W' := Q)
              ¬∑ exact Q.pullback_fst B.hom f hQf
              ¬∑ simpa using g.prop_hom_left
          invFun := fun h ‚Ü¶ Over.homMk (h.left ‚â´ pullback.fst B.hom f)
            (by
              simp only [map_obj_left, Functor.const_obj_obj, pullback_obj_left, Functor.id_obj,
                Category.assoc, pullback.condition, map_obj_hom, ‚Üê pullback_obj_hom, Over.w_assoc])
            (Q.comp_mem _ _ h.prop_hom_left (Q.pullback_fst _ _ hQf))
          left_inv := by cat_disch
          right_inv := fun h ‚Ü¶ by
            ext
            dsimp
            ext
            ¬∑ simp
            ¬∑ simpa using h.w.symm } }

end Adjunction

/-- Pushforward along a morphism `f` (for which all pullbacks exist) exists relative to `P`
when pushforwards exist along `f` for all morphisms satisfying `P`. -/
protected abbrev HasPushforward (P : MorphismProperty T) {S S' : T} (f : S ‚ü∂ S')
    [‚àÄ {W} (h : W ‚ü∂ S'), HasPullback h f] : Prop :=
  ‚àÄ {W} (h : W ‚ü∂(P) S), HasPushforward f (.mk h.1)

/-- Morphisms satisfying `P` have pushforwards along morphisms satisfying `Q`.
We require that `[H.HasPullbacks]` so that we can define the universal property of
pushforward along `p` relative to the pullback.
-/
protected abbrev HasPushforwards (P : MorphismProperty T)
    (Q : MorphismProperty T) [Q.HasPullbacks] : Prop :=
  ‚àÄ {S S' : T} (q : S ‚ü∂(Q) S'), P.HasPushforward q.1

/-- Morphisms satisfying `P` are stable under pushforward along morphisms satisfying `Q`
if whenever pushforward along a morphism in `Q` exists it is in `P`. -/
class IsStableUnderPushforward (P : MorphismProperty T)
    (Q : MorphismProperty T) [Q.HasPullbacks] : Prop where
  of_isPushforward {S S' X Y : T} (q : S ‚ü∂(Q) S') (f : X ‚ü∂(P) S) (g : Y ‚ü∂ S')
    (isPushforward : IsPushforward q.1 (.mk f.1) (.mk g)) : P g

noncomputable section

/-- If `P` has pushforwards along `q` then there is a partial left adjoint `P.Over ‚ä§ S ‚•§ Over S'`
of the pullback functor `pullback q : Over S' ‚•§ Over S`.
-/
noncomputable def pushforwardPartial (P : MorphismProperty T)
    {S S' : T} (q : S ‚ü∂ S') [‚àÄ {W} (h : W ‚ü∂ S'), HasPullback h q] [P.HasPushforward q] :
    P.Over ‚ä§ S ‚•§ Over S' := by
  refine Functor.PartialRightAdjointSource.lift (Over.forget P ‚ä§ S) ?_ ‚ãô
    (CategoryTheory.Over.pullback q).partialRightAdjoint
  intro X
  let X' : _ ‚ü∂(P) S := ‚ü® X.hom , X.prop ‚ü©
  convert_to ((CategoryTheory.Over.pullback q).op ‚ãô
    yoneda.obj (CategoryTheory.Over.mk X'.fst)).IsRepresentable
  infer_instance

-- section homEquiv

-- variable {P} {S S' : T} (q : S ‚ü∂ S')
--     [‚àÄ {W} (h : W ‚ü∂ S'), HasPullback h q] [P.HasPushforward q] {X : Over S'} {Y : P.Over ‚ä§ S}

-- /-- The pushforward functor is a partial right adjoint to pullback in the sense that
-- there is a natural bijection of hom-sets
-- `T / S (pullback q X, Y) ‚âÉ T / S' (X, pushforward q Y)`. -/
-- abbrev pushforwardPartial.homEquiv :
--     (X ‚ü∂ (pushforwardPartial P q).obj Y) ‚âÉ
--     ((CategoryTheory.Over.pullback q).obj X ‚ü∂ Y.toComma) :=
--   Functor.partialRightAdjointHomEquiv _

-- lemma pushforwardPartial.homEquiv_comp {S S' : T} (q : S ‚ü∂ S')
--     [‚àÄ {W} (h : W ‚ü∂ S'), HasPullback h q] [P.HasPushforward q] {X X' : Over S'} {Y : P.Over ‚ä§ S}
--     (f : X' ‚ü∂ (pushforwardPartial P q).obj Y) (g : X ‚ü∂ X') :
--     pushforwardPartial.homEquiv q (g ‚â´ f) =
--     (CategoryTheory.Over.pullback q).map g ‚â´ pushforwardPartial.homEquiv q f :=
--   Functor.partialRightAdjointHomEquiv_comp ..

-- lemma pushforwardPartial.homEquiv_map_comp {S S' : T} (q : S ‚ü∂ S')
--     [‚àÄ {W} (h : W ‚ü∂ S'), HasPullback h q] [P.HasPushforward q] {X : Over S'} {Y Y' : P.Over ‚ä§ S}
--     (f : X ‚ü∂ (pushforwardPartial P q).obj Y) (g : Y ‚ü∂ Y') :
--     pushforwardPartial.homEquiv q (f ‚â´ (P.pushforwardPartial q).map g) =
--     pushforwardPartial.homEquiv q f ‚â´ g.toCommaMorphism :=
--   Functor.partialRightAdjointHomEquiv_map_comp ..

-- end homEquiv

/-- When `P` has pushforwards along `Q` and is stable under pushforwards along `Q`,
the pushforward functor along any morphism `q` satisfying `Q` can be defined. -/
noncomputable def pushforward {Q : MorphismProperty T} [Q.HasPullbacks] [P.HasPushforwards Q]
    [P.IsStableUnderPushforward Q] {S S' : T} (q : S ‚ü∂(Q) S') : P.Over ‚ä§ S ‚•§ P.Over ‚ä§ S' :=
  Comma.lift (pushforwardPartial P q.1) (fun X =>
    let X' : _ ‚ü∂(P) S := ‚ü® X.hom , X.prop ‚ü©
    IsStableUnderPushforward.of_isPushforward q X' _
        (pushforward.isPushforward q.fst (CategoryTheory.Over.mk X'.fst)))
  (by simp) (by simp)

section homEquiv

variable {P} {Q : MorphismProperty T} [Q.HasPullbacks] [P.HasPushforwards Q]
  [P.IsStableUnderPushforward Q] {S S' : T} (q : S ‚ü∂(Q) S')

/-- The pushforward functor is a partial right adjoint to pullback in the sense that
there is a natural bijection of hom-sets `T / S (pullback q X, Y) ‚âÉ T / S' (X, pushforward q Y)`. -/
def pushforward.homEquiv {X : Over S'} {Y : P.Over ‚ä§ S} :
    (X ‚ü∂ ((pushforward P q).obj Y).toComma) ‚âÉ
    ((CategoryTheory.Over.pullback q.1).obj X ‚ü∂ Y.toComma) :=
  (Functor.partialRightAdjointHomEquiv ..)

lemma pushforward.homEquiv_comp {X X' : Over S'} {Y : P.Over ‚ä§ S}
    (f : X' ‚ü∂ ((pushforward P q).obj Y).toComma) (g : X ‚ü∂ X') :
    pushforward.homEquiv q (g ‚â´ f) =
    (CategoryTheory.Over.pullback q.fst).map g ‚â´ homEquiv q f :=
  Functor.partialRightAdjointHomEquiv_comp ..

lemma pushforward.homEquiv_map_comp {X : Over S'} {Y Y' : P.Over ‚ä§ S}
    (f : X ‚ü∂ ((pushforward P q).obj Y).toComma) (g : Y ‚ü∂ Y') :
    homEquiv q (f ‚â´ Comma.Hom.hom ((P.pushforward q).map g)) =
    homEquiv q f ‚â´ Comma.Hom.hom g :=
  Functor.partialRightAdjointHomEquiv_map_comp ..

lemma pushforward.homEquiv_symm_comp {X : Over S'} {Y Y' : P.Over ‚ä§ S}
    (f : (CategoryTheory.Over.pullback q.1).obj X ‚ü∂ Y.toComma) (g : Y ‚ü∂ Y') :
    (homEquiv q).symm f ‚â´ Comma.Hom.hom ((P.pushforward q).map g) =
    (homEquiv q).symm (f ‚â´ Comma.Hom.hom g) :=
  Functor.partialRightAdjointHomEquiv_symm_comp ..

lemma pushforward.homEquiv_comp_symm {X X' : Over S'} {Y : P.Over ‚ä§ S}
    (f : (CategoryTheory.Over.pullback q.1).obj X' ‚ü∂ Y.toComma) (g : X ‚ü∂ X') :
    g ‚â´ (homEquiv q).symm f =
    (homEquiv q).symm ((CategoryTheory.Over.pullback q.fst).map g ‚â´ f) :=
  Functor.partialRightAdjointHomEquiv_comp_symm ..

end homEquiv

section

open MorphismProperty.Over

variable [P.IsStableUnderBaseChange] {S S' : T} (f : S ‚ü∂(Q) S')
    [Q.HasPullbacks] [P.HasPushforwards Q] [P.IsStableUnderPushforward Q]

/-- The `pullback ‚ä£ pushforward` adjunction. -/
def pullbackPushforwardAdjunction : pullback P ‚ä§ f.1 ‚ä£ pushforward P f :=
  Adjunction.mkOfHomEquiv {
    homEquiv X Y :=
      calc ((pullback P ‚ä§ f.1).obj X ‚ü∂ Y)
      _ ‚âÉ (((Over.pullback P ‚ä§ f.fst).obj X).toComma ‚ü∂ Y.toComma) :=
        (Functor.FullyFaithful.ofFullyFaithful (Over.forget P ‚ä§ S)).homEquiv
      _ ‚âÉ (X.toComma ‚ü∂ ((P.pushforward f).obj Y).toComma) :=
        (pushforward.homEquiv f).symm
      _ ‚âÉ _ := Equiv.cast (by dsimp) -- why?
      _ ‚âÉ (X ‚ü∂ (P.pushforward f).obj Y) :=
        (Functor.FullyFaithful.ofFullyFaithful (Over.forget P ‚ä§ S')).homEquiv.symm
    homEquiv_naturality_left_symm g f := by
      simp only [Equiv.trans_def, Equiv.cast_refl, Equiv.trans_refl,
        Equiv.symm_trans_apply, Equiv.symm_symm]
      erw [Functor.FullyFaithful.homEquiv_apply, Functor.FullyFaithful.homEquiv_symm_apply,
        Functor.FullyFaithful.homEquiv_apply, Functor.FullyFaithful.homEquiv_symm_apply,
        Functor.map_comp, pushforward.homEquiv_comp]
      apply Functor.FullyFaithful.map_injective
        (Functor.FullyFaithful.ofFullyFaithful (Over.forget P ‚ä§ S))
      simp only [Functor.FullyFaithful.map_preimage, Functor.map_comp]
      simp only [Comma.forget_obj, Comma.forget_map, hom_pullback_map]
      congr 1
    homEquiv_naturality_right f g := by
      simp only [Comma.forget_obj, Equiv.trans_def, Equiv.cast_refl, Equiv.trans_refl,
        Equiv.trans_apply]
      erw [Functor.FullyFaithful.homEquiv_symm_apply, Functor.FullyFaithful.homEquiv_symm_apply,
        Functor.FullyFaithful.homEquiv_apply, Functor.FullyFaithful.homEquiv_apply]
      apply Functor.FullyFaithful.map_injective
        (Functor.FullyFaithful.ofFullyFaithful (Over.forget P ‚ä§ S'))
      simp only [Functor.FullyFaithful.map_preimage, Functor.map_comp]
      erw [pushforward.homEquiv_symm_comp]
      rfl
  }

instance : (pullback P ‚ä§ f.1).IsLeftAdjoint :=
  Adjunction.isLeftAdjoint (pullbackPushforwardAdjunction P Q f)

instance : (pushforward P f).IsRightAdjoint :=
  Adjunction.isRightAdjoint (pullbackPushforwardAdjunction P Q f)

end

section homEquiv

variable {P} [P.HasPullbacks] [P.IsStableUnderBaseChange] {S S' : T} (i : S ‚ü∂ S')

/-- `MorphismProperty.Over.pullback P ‚ä§ f` is a partial right adjoint to `Over.map f`. -/
@[simps!]
def pullback.homEquiv {X : Over S} {Y : P.Over ‚ä§ S'} :
    (X ‚ü∂ ((Over.pullback P ‚ä§ i).obj Y).toComma) ‚âÉ
    ((CategoryTheory.Over.map i).obj X ‚ü∂ Y.toComma) where
  toFun v := CategoryTheory.Over.homMk (v.left ‚â´ pullback.fst _ _) <| by
            simp only [Over.morphismProperty_fst, Category.assoc, pullback.condition,
              CategoryTheory.Over.map_obj_hom]
            erw [‚Üê CategoryTheory.Over.w v]
            simp
  invFun u := CategoryTheory.Over.homMk (pullback.lift u.left X.hom <| by simp)
  left_inv v := by
    ext; dsimp; ext
    ¬∑ simp
    ¬∑ simpa using (CategoryTheory.Over.w v).symm
  right_inv u := by cat_disch

lemma pullback.homEquiv_comp {X X' : Over S} {Y : P.Over ‚ä§ S'}
    (f : X' ‚ü∂ ((Over.pullback P ‚ä§ i).obj Y).toComma) (g : X ‚ü∂ X') :
    homEquiv i (g ‚â´ f) =
    (CategoryTheory.Over.map i).map g ‚â´ homEquiv i f := by
  ext; simp

lemma pullback.homEquiv_map_comp {X : Over S} {Y Y' : P.Over ‚ä§ S'}
    (f : X ‚ü∂ ((Over.pullback P ‚ä§ i).obj Y).toComma) (g : Y ‚ü∂ Y') :
    homEquiv i (f ‚â´ Comma.Hom.hom ((Over.pullback P ‚ä§ i).map g)) =
    homEquiv i f ‚â´ Comma.Hom.hom g := by
  ext; simp

lemma pullback.homEquiv_symm_comp {X : Over S} {Y Y' : P.Over ‚ä§ S'}
    (f : (CategoryTheory.Over.map i).obj X ‚ü∂ Y.toComma) (g : Y ‚ü∂ Y') :
    (homEquiv i).symm f ‚â´ Comma.Hom.hom ((Over.pullback P ‚ä§ i).map g) =
    (homEquiv i).symm (f ‚â´ Comma.Hom.hom g) := by
  ext; dsimp; ext
  ¬∑ simp
  ¬∑ simp

lemma pullback.homEquiv_comp_symm {X X' : Over S} {Y : P.Over ‚ä§ S'}
    (f : (CategoryTheory.Over.map i).obj X' ‚ü∂ Y.toComma) (g : X ‚ü∂ X') :
    g ‚â´ (homEquiv i).symm f =
    (homEquiv i).symm ((CategoryTheory.Over.map i).map g ‚â´ f) := by
  ext; dsimp; ext
  ¬∑ simp
  ¬∑ simp

end homEquiv

end

end CategoryTheory.MorphismProperty
