/-
Copyright (c) 2025 Joseph Hua. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Hua, Sina Hazratpour, Emily Riehl
-/

import Mathlib.CategoryTheory.MorphismProperty.OverAdjunction
import Mathlib.CategoryTheory.Functor.TwoSquare
import Mathlib.CategoryTheory.NatTrans.IsCartesian
import Mathlib.CategoryTheory.Comma.Over.Pushforward
import HoTTLean.ForMathlib

universe v u vâ‚ uâ‚

noncomputable section

namespace CategoryTheory

open Category Limits MorphismProperty

variable {C : Type u} [Category.{v} C]

namespace MorphismProperty

namespace PolynomialPartialAdjunction

variable {T : Type u} [Category.{v} T] {R : MorphismProperty T}
  [R.HasPullbacks] [R.IsStableUnderBaseChange]
  {Q : MorphismProperty T} [Q.HasPullbacks] [R.HasPushforwards Q]
  [R.IsStableUnderPushforward Q]
  {E I B : T} (i : E âŸ¶ I) (p : E âŸ¶(Q) B)

/-- The partial right adjoint representing a multivariate polynomial. -/
abbrev partialRightAdjoint := Over.pullback R âŠ¤ i â‹™ pushforward R p

/-- The left adjoint in the partial adjunction. -/
abbrev leftAdjoint := CategoryTheory.Over.pullback p.1 â‹™ CategoryTheory.Over.map i

/-- `pullback R âŠ¤ i â‹™ pushforward R p` is a partial right adjoint to
`CategoryTheory.Over.pullback p.1 â‹™ CategoryTheory.Over.map i`
  ```
         pullback i       pushforward p
   R.Over I ------> R.Over E -----> R.Over B
      |               |                |
      |       âŠ¥       |        âŠ¥       |
      |               |                |
      V               V                V
     C/I <--------- C/E <------------ C/B
            map i         pullback p
  ```

On paper this is written `C/B (X, pâ (i* Y)) â‰ƒ C/I (i! (p* X), Y)`.
-/
def homEquiv {X : Over B} {Y : R.Over âŠ¤ I} :
    (X âŸ¶ ((partialRightAdjoint i p).obj Y).toComma) â‰ƒ
    ((leftAdjoint i p).obj X âŸ¶ Y.toComma) :=
  calc (X âŸ¶ ((R.pushforward p).obj ((Over.pullback R âŠ¤ i).obj Y)).toComma)
  _ â‰ƒ ((CategoryTheory.Over.pullback p.1).obj X âŸ¶ ((Over.pullback R âŠ¤ i).obj Y).toComma) :=
    pushforward.homEquiv ..
  _ â‰ƒ ((CategoryTheory.Over.map i).obj
      ((CategoryTheory.Over.pullback p.fst).obj X) âŸ¶ Y.toComma) :=
    pullback.homEquiv ..

lemma homEquiv_comp {X X' : Over B} {Y : R.Over âŠ¤ I}
    (f : X' âŸ¶ ((partialRightAdjoint i p).obj Y).toComma) (g : X âŸ¶ X') :
    homEquiv i p (g â‰« f) =
    (leftAdjoint i p).map g â‰« homEquiv i p f := by
  unfold homEquiv
  simp only [Functor.comp_obj, Equiv.trans_def, Equiv.trans_apply]
  erw [pushforward.homEquiv_comp, pullback.homEquiv_comp]
  rfl

lemma homEquiv_map_comp {X : Over B} {Y Y' : R.Over âŠ¤ I}
    (f : X âŸ¶ ((partialRightAdjoint i p).obj Y).toComma) (g : Y âŸ¶ Y') :
    homEquiv i p (f â‰« Comma.Hom.hom ((partialRightAdjoint i p).map g)) =
    homEquiv i p f â‰« Comma.Hom.hom g := by
  unfold homEquiv
  simp only [Functor.comp_obj, Equiv.trans_def, Equiv.trans_apply]
  erw [pushforward.homEquiv_map_comp, pullback.homEquiv_map_comp]
  rfl

lemma homEquiv_symm_comp {X : Over B} {Y Y' : R.Over âŠ¤ I}
    (f : (leftAdjoint i p).obj X âŸ¶ Y.toComma) (g : Y âŸ¶ Y') :
    (homEquiv i p).symm f â‰« Comma.Hom.hom ((partialRightAdjoint i p).map g) =
    (homEquiv i p).symm (f â‰« Comma.Hom.hom g) := by
  unfold homEquiv
  simp
  erw [pushforward.homEquiv_symm_comp, pullback.homEquiv_symm_comp]
  rfl

lemma homEquiv_comp_symm {X X' : Over B} {Y : R.Over âŠ¤ I}
    (f : (leftAdjoint i p).obj X' âŸ¶ Y.toComma) (g : X âŸ¶ X') :
    g â‰« (homEquiv i p).symm f =
    (homEquiv i p).symm ((leftAdjoint i p).map g â‰« f) := by
  unfold homEquiv
  simp
  erw [pushforward.homEquiv_comp_symm, pullback.homEquiv_comp_symm]
  rfl

/-- The counit of the partial adjunction is given by evaluating the equivalence of
hom-sets at the identity.
On paper we write this as `counit : i! p* pâˆ— i* => Over.forget : R.Over âŠ¤ I â¥¤ Over I`
-/
def counit :
    partialRightAdjoint i p â‹™ Over.forget R âŠ¤ B â‹™ leftAdjoint i p âŸ¶ Over.forget R âŠ¤ I where
  app _ := homEquiv i p (ğŸ™ _)
  naturality X Y f := by
    apply (homEquiv i p).symm.injective
    conv => left; erw [â† homEquiv_comp_symm]
    conv => right; erw [â† homEquiv_symm_comp]
    simp

end PolynomialPartialAdjunction

variable (P : MorphismProperty C)

namespace Over

@[simps]
def equivalenceOfHasObjects' (R : MorphismProperty C) [R.HasObjects]
    {X : C} (hX : IsTerminal X) : R.Over âŠ¤ X â‰Œ Over X where
  functor := MorphismProperty.Over.forget _ _ _
  inverse := Comma.lift (ğŸ­ _) (by intro; apply HasObjects.obj_mem _ hX) (by simp) (by simp)
  unitIso := eqToIso rfl
  counitIso := eqToIso rfl
  functor_unitIso_comp := by simp

@[simp]
def equivalenceOfHasObjects (R : MorphismProperty C) [R.HasObjects]
    {X : C} (hX : IsTerminal X) : R.Over âŠ¤ X â‰Œ C :=
  (equivalenceOfHasObjects' R hX).trans (Over.equivalenceOfIsTerminal hX)

variable {P : MorphismProperty C} {E B : C}

@[simps]
def ofMorphismProperty (p : E âŸ¶(P) B) : P.Over âŠ¤ B where
  left := E
  right := âŸ¨âŸ¨âŸ©âŸ©
  hom := p.1
  prop := p.2

@[simps]
def homMkTop {p q : P.Over âŠ¤ B} (left : p.left âŸ¶ q.left) (hleft : left â‰« q.hom = p.hom) :
    p âŸ¶ q where
  left := left
  right := eqToHom (by simp)
  w := by simp [hleft]
  prop_hom_left := trivial
  prop_hom_right := trivial

/--
Convert an object `p` in `R.Over âŠ¤ B` to a morphism in `R.Over âŠ¤ O` by composing with `o`.
     p
 E -----> B
  \      /
   \    /o
    \  /
     VV
     O
-/
@[simp]
def homOfMorphismProperty [P.IsStableUnderComposition] {O} (p : P.Over âŠ¤ B) (o : B âŸ¶(P) O) :
    (map âŠ¤ o.2).obj p âŸ¶ Over.ofMorphismProperty o :=
  Over.homMk p.hom (by simp)

end Over

end MorphismProperty

open MorphismProperty.Over

/-- `P : MvPoly R H I O` is a the signature for a multivariate polynomial functor,
consisting of the following maps
```
         p
      E ---> B
  i â†™         â†˜ o
  I               O
```
We can lazily read this as `âˆ‘ b : B, X ^ (E b)`,
for some `X` in the (`P`-restricted) slice over `I`.

In full detail:
Viewing such an `X` as a series of variables `X_k` indexed by `k âˆˆ I`,
and `B` as a family of types `B_k` indexed by `j âˆˆ O`
this can be further viewed as `O`-many `I`-ary polynomials `âˆ‘ b : B_j, X_(i b) ^ (E b)`

To explain the need for two morphism properties,
consider the following two use-cases:
1. `R = âŠ¤` is all maps and the category has all pullbacks.
  `H` is the class of exponentiable maps - it follows from all maps having pullbacks that `H`
  also has pullbacks.
2. `R = H` is a Ï€-clan, [see Joyal, def 2.4.1](https://arxiv.org/pdf/1710.10238).

This will typically be used with the following instances

- For pullback of `R`-maps along `i`, `p` and `o` we need
  `[R.IsStableUnderBaseChange] [R.HasPullbacks]`
- For the left adjoint to pullback for `o` we need `[R.IsStableUnderComposition]`
- For pushforward of `R`-maps along `p` we need
  `[R.IsStableUnderPushforward H] [R.HasPushforwards H]`
- For pushforward of `R`-maps along `p` we also assume `[H.HasPullbacks]`.
  This is useful - it makes the `R`-restricted pushforward of `R`-maps along `p`
  a partial left adjoint to *global* pullback along `p`,
  ```
        pushforward p
   R.Over E -----> R.Over B
      |              |
      |       âŠ¥      |
      |              |
      V              V
     C/E <--------- C/B
         pullback p
  ```
  which is strictly stronger than just having a left adjoint to `R`-restricted pullback
  `(pullback : R.Over B â¥¤ R.Over E) âŠ£ (pushforward : R.Over E â¥¤ R.Over B)`.
-/
structure MvPoly (R : MorphismProperty C) (H : MorphismProperty C) (I O : C) where
  (E B : C)
  (i : E âŸ¶(R) I)
  (p : E âŸ¶(H) B)
  (o : B âŸ¶(R) O)

namespace MvPoly

instance : (âŠ¤ : MorphismProperty C).HasOfPostcompProperty âŠ¤ where
  of_postcomp := by simp

variable {R : MorphismProperty C} {H : MorphismProperty C}

instance {B O : C} (i : B âŸ¶(R) O) [R.HasPullbacks] [R.IsStableUnderBaseChange]
    [R.IsStableUnderComposition] : (pullback R âŠ¤ i.1).IsRightAdjoint :=
  (mapPullbackAdj R âŠ¤ i.1 i.2 âŸ¨âŸ©).isRightAdjoint

variable {I O : C} (P : MvPoly R H I O) [R.HasPullbacks] [R.IsStableUnderBaseChange]
    [H.HasPullbacks] [R.HasPushforwards H]
    [R.IsStableUnderPushforward H]

open PolynomialPartialAdjunction

/-- (Ignoring the indexing from `i` and `o`)
This is the first projection morphism from `P @ X = âˆ‘ b : B, X ^ (E b)` to `B`,
as an object in the `P`-restricted slice over `B`. -/
abbrev fstProj (P : MvPoly R H I O) (X : R.Over âŠ¤ I) : R.Over âŠ¤ P.B :=
  (partialRightAdjoint P.i.1 P.p).obj X

/-- The counit of the adjunction `pullback p â‹™ map i âŠ£ pullback i â‹™ pushforward p` evaluated at `X`.
Ignoring the indexing from `i` and `o`,
this can be viewed as the second projection morphism from `P @ X = âˆ‘ b : B, X ^ (E b)`
to `X^ (E b)`.

```
     X ----------> I
     âˆ§             âˆ§
     |             |
 sndProj           | i
     |             |
     â€¢ ----------> E
     |             |
     |    (pb)     |
     |             |p
     V    fstProj  V
   P @ X --------> B
       âŸ          |
          âŸ       |o
             âŸ    |
                â†˜ V
                   O
```
-/
def sndProj (P : MvPoly R H I O) (X : R.Over âŠ¤ I) :
    (leftAdjoint P.i.1 P.p).obj (fstProj P X).toComma âŸ¶ X.toComma :=
  (counit P.i.1 P.p).app X

section

variable (P : MvPoly R H I O) {X Y : R.Over âŠ¤ I} (f : X âŸ¶ Y)

@[reassoc (attr := simp)]
lemma map_fstProj :
    ((partialRightAdjoint P.i.1 P.p).map f).left â‰« (fstProj P Y).hom = (fstProj P X).hom := by
  simp

lemma sndProj_comp_hom : (sndProj P X).left â‰« X.hom = pullback.snd _ _ â‰« P.i.1 := by
  simp [sndProj]

lemma sndProj_comp : (sndProj P X).left â‰« f.left =
    pullback.map _ _ _ _
      ((partialRightAdjoint P.i.1 P.p).map f).left (ğŸ™ _) (ğŸ™ _) (by simp) (by simp) â‰«
      (sndProj P Y).left := by
  have := congr_arg CommaMorphism.left <| (counit P.i.1 P.p).naturality f
  simpa [pullback.map] using this.symm

end

variable [R.IsStableUnderComposition]
/-- A multivariate polynomial signature
```
         p
      E ---> B
  i â†™         â†˜ o
  I               O
```
gives rise to a functor
```
                         pushforward p
                R.Over âŠ¤ E ---------> R.Over âŠ¤ B
     pullback i â†—                              âŸ map o
             âŸ‹                                    âŸ
          âŸ‹                                          â†˜
  R.Over âŠ¤ I                                      R.Over âŠ¤ O
```
-/
def functor : R.Over âŠ¤ I â¥¤ R.Over âŠ¤ O :=
  pullback R âŠ¤ P.i.1 â‹™ MorphismProperty.pushforward R P.p â‹™ map âŠ¤ P.o.2

/-- The action of a univariate polynomial on objects. -/
def apply (P : MvPoly R H I O) : R.Over âŠ¤ I â†’ R.Over âŠ¤ O := (functor P).obj

@[inherit_doc]
infix:90 " @ " => apply

namespace Equiv

variable {P : MvPoly R H I O} {Î“ : Over O} {X : R.Over âŠ¤ I}

def fst (pair : Î“ âŸ¶ (P @ X).toComma) : Over P.B := Over.mk (pair.left â‰« (fstProj P X).hom)

abbrev sndDom (pair : Î“ âŸ¶ (P @ X).toComma) : Over I := (leftAdjoint P.i.1 P.p).obj (fst pair)

def snd (pair : Î“ âŸ¶ (P @ X).toComma) : sndDom pair âŸ¶ X.toComma :=
  homEquiv P.i.1 P.p (Over.homMk (pair.left))

lemma snd_eq (pair : Î“ âŸ¶ (P @ X).toComma) : snd pair =
    (leftAdjoint P.i.1 P.p).map (Over.homMk (pair.left)) â‰« sndProj P X := by
  erw [Equiv.apply_eq_iff_eq_symm_apply, â† homEquiv_comp_symm]
  simp [sndProj, counit]

def mk (f : Over P.B) (hf : Î“ = (Over.map P.o.1).obj f)
    (s : (leftAdjoint P.i.1 P.p).obj f âŸ¶ X.toComma) :
    Î“ âŸ¶ (P @ X).toComma :=
  eqToHom hf â‰« (Over.map P.o.fst).map ((homEquiv P.i.1 P.p).symm s)

@[simp]
lemma fst_mk (f : Over P.B) (hf : Î“ = (Over.map P.o.1).obj f)
    (s : (leftAdjoint P.i.1 P.p).obj f âŸ¶ X.toComma) : fst (mk f hf s) = f := by
  subst hf; simp [fst, mk]; rfl

lemma snd_mk (f : Over P.B) (hf : Î“ = (Over.map P.o.1).obj f)
    (s : (leftAdjoint P.i.1 P.p).obj f âŸ¶ X.toComma) : snd (mk f hf s) =
    eqToHom (by simp) â‰« s := calc snd (mk f hf s)
  _ = (leftAdjoint P.i.1 P.p).map (eqToHom (fst_mk f hf s)) â‰« s := by
    erw [Equiv.apply_eq_iff_eq_symm_apply, â† homEquiv_comp_symm]
    ext
    simp [mk]
  _ = eqToHom _ â‰« s := by
    simp only [eqToHom_map]

@[simp]
lemma map_fst (pair : Î“ âŸ¶ (P @ X).toComma) : (Over.map P.o.fst).obj (fst pair) = Î“ := by
  have := pair.w
  simp only [Functor.id_obj, Functor.const_obj_obj, Functor.id_map,
    CostructuredArrow.right_eq_id, Functor.const_obj_map, comp_id] at this
  simp [Over.map, Comma.mapRight, fst]
  congr

@[simp]
lemma eta (pair : Î“ âŸ¶ (P @ X).toComma) : mk (fst pair) (by simp) (snd pair) = pair := by
  ext
  simp [mk, snd]

end Equiv

instance (P : MvPoly R H I O) : Limits.PreservesLimitsOfShape WalkingCospan
    (MorphismProperty.Over.map âŠ¤ P.o.2) := by sorry

instance (P : MvPoly R H I O) :
    Limits.PreservesLimitsOfShape WalkingCospan (MvPoly.functor P) := by
  dsimp [functor]
  have : (MorphismProperty.Over.pullback R âŠ¤ P.i.1).IsRightAdjoint :=
    Adjunction.isRightAdjoint (MorphismProperty.Over.mapPullbackAdj R âŠ¤ P.i.1 P.i.2 trivial)
  infer_instance

end MvPoly

/-- `P : UvPoly R E B` is the type of signatures for polynomial functors
         p
      E ---> B

We read this as `âˆ‘ b : B, X ^ (E b)`,
for some `R`-object `X` (meaning the unique map to the terminal object is in `R`).

This notion of polynomial makes sense when `R` is a Ï€-clan,
[see Joyal, def 2.4.1](https://arxiv.org/pdf/1710.10238).
Therefore it will typically be used with the following instances

- For pullback of `R`-maps along `p` we need
  `[R.IsStableUnderBaseChange] [R.HasPullbacks]`
- For the left adjoint to pullback along `B`, we assume `[R.IsStableUnderComposition]`
  and `[R.HasObjects]`, meaning the unique map `B âŸ¶ âŠ¤_ C` is in `R`.
  For this, we will also assume `[HasTerminal C]`.
- For pushforward of `R`-maps along `p` we need
  `[R.IsStableUnderPushforward R] [R.HasPushforwards R]`
- For pushforward of `R`-maps along `p` we also assume `[R.HasPullbacks]`.
  This is useful - it makes the `R`-restricted pushforward of `R`-maps along `p`
  a partial left adjoint to *global* pullback along `p`,
  ```
        pushforward p
   R.Over E -----> R.Over B
      |              |
      |       âŠ¥      |
      |              |
      V              V
     C/E <--------- C/B
         pullback p
  ```
  which is strictly stronger than just having a left adjoint to `R`-restricted pullback
  `(pullback : R.Over B â¥¤ R.Over E) âŠ£ (pushforward : R.Over E â¥¤ R.Over B)`.
-/
structure UvPoly (R : MorphismProperty C) (E B : C) where
  (p : E âŸ¶ B)
  (morphismProperty : R p)

namespace UvPoly

section

variable {R : MorphismProperty C} {E B : C}

variable [HasTerminal C]

variable [R.IsStableUnderComposition] [R.HasPullbacks] [R.IsStableUnderBaseChange] [R.HasObjects]
  [R.IsStableUnderPushforward R] [R.HasPushforwards R]

instance (P : UvPoly R E B) {Î“ : C} (A : Î“ âŸ¶ B) : HasPullback A P.p := by
  let p : E âŸ¶(R) B := âŸ¨ P.p, P.morphismProperty âŸ©
  convert_to HasPullback A p.1
  apply MorphismProperty.instHasPullbackFstHomOfHasPullbacks

instance (P : UvPoly R E B) {Î“ : C} (A : Î“ âŸ¶ B) : HasPullback P.p A :=
  hasPullback_symmetry _ _

def object (X : C) : X âŸ¶(R) âŠ¤_ C :=
  âŸ¨terminal.from X, HasObjects.obj_mem _ terminalIsTerminalâŸ©

@[simp]
abbrev toOverTerminal : C â¥¤ R.Over âŠ¤ (âŠ¤_ C) :=
  (equivalenceOfHasObjects R terminalIsTerminal).inverse

@[simp]
abbrev fromOverTerminal : R.Over âŠ¤ (âŠ¤_ C) â¥¤ C :=
  (equivalenceOfHasObjects R terminalIsTerminal).functor

@[simps]
def mvPoly (P : UvPoly R E B) : MvPoly R R (âŠ¤_ C) (âŠ¤_ C) where
  E := E
  B := B
  i := object E
  p := âŸ¨P.p, P.morphismPropertyâŸ©
  o := object B

def functor (P : UvPoly R E B) : C â¥¤ C :=
  toOverTerminal â‹™
  MvPoly.functor P.mvPoly â‹™
  fromOverTerminal

/-- The action of a univariate polynomial on objects. -/
def apply [HasTerminal C] (P : UvPoly R E B) : C â†’ C := P.functor.obj

@[inherit_doc]
infix:90 " @ " => apply

instance [HasTerminal C] (P : UvPoly R E B) :
    Limits.PreservesLimitsOfShape WalkingCospan P.functor := by
  unfold functor
  infer_instance

variable (B)

/-- The identity polynomial functor in single variable. -/
@[simps!]
def id (R : MorphismProperty C) [R.ContainsIdentities] (B) : UvPoly R B B := âŸ¨ğŸ™ B, R.id_mem _ âŸ©

@[simps!]
def vcomp [R.IsStableUnderComposition] {A B C} (P : UvPoly R A B) (Q : UvPoly R B C) :
    UvPoly R A C :=
  âŸ¨ P.p â‰« Q.p, R.comp_mem _ _ P.morphismProperty Q.morphismProperty âŸ©

variable {B}

/-- The fstProjection morphism from `âˆ‘ b : B, X ^ (E b)` to `B` again. -/
def fstProj (P : UvPoly R E B) (X : C) : P @ X âŸ¶ B :=
  (P.mvPoly.fstProj (toOverTerminal.obj X)).hom

@[reassoc (attr := simp)]
lemma map_fstProj (P : UvPoly R E B) {X Y : C} (f : X âŸ¶ Y) :
    P.functor.map f â‰« fstProj P Y = fstProj P X :=
  P.mvPoly.map_fstProj (toOverTerminal.map f)

/-- The second projection morphism from `P @ X = âˆ‘ b : B, X ^ (E b)` to `X^ (E b)`. -/
def sndProj (P : UvPoly R E B) (X : C) :
    Limits.pullback (fstProj P X) P.p âŸ¶ X :=
  (P.mvPoly.sndProj (toOverTerminal.obj X)).left

lemma sndProj_comp (P : UvPoly R E B) {X Y : C} (f : X âŸ¶ Y) :
    sndProj P X â‰« f =
    pullback.map _ _ _ _ (P.functor.map f) (ğŸ™ _) (ğŸ™ _) (by simp) (by simp) â‰« sndProj P Y :=
  P.mvPoly.sndProj_comp (toOverTerminal.map f)

open TwoSquare

/-- A vertical map `Ï : P.p.1 âŸ¶ Q.p.1` of polynomials (i.e. a commutative triangle)
```
    Ï
E ----> F
 \     /
  \   / \ /
    B
```
induces a natural transformation `Q.functor âŸ¶ P.functor ` obtained by pasting the following 2-cells
```
              Q.p.1
C --- >  C/F ----> C/B -----> C
|         |          |        |
|   â†™     | Ï*  â‰…    |   =    |
|         v          v        |
C --- >  C/E ---->  C/B ----> C
              P.p.1
```
-/
def verticalNatTrans {F : C} (P : UvPoly R E B) (Q : UvPoly R F B) (Ï : E âŸ¶ F)
    (h : P.p = Ï â‰« Q.p) : Q.functor âŸ¶ P.functor := sorry --by
  -- have sq : CommSq Ï P.p.1 Q.p.1 (ğŸ™ _) := by simp [h]
  -- let cellLeft := (Over.starPullbackIsoStar Ï).hom
  -- let cellMid := (pushforwardPullbackTwoSquare Ï P.p Q.p (ğŸ™ _) sq)
  -- let cellLeftMidPasted := TwoSquare.whiskerRight (cellLeft â‰«â‚• cellMid) (Over.pullbackId).inv
  -- simpa using (cellLeftMidPasted â‰«â‚• (vId (forget B)))

/-- A cartesian map of polynomials
```
           P.p
      E -------->  B
      |            |
   Ï†  |            | Î´
      v            v
      F -------->  D
           Q.p
```
induces a natural transformation between their associated functors obtained by pasting the following
2-cells
```
              Q.p
C --- >  C/F ----> C/D -----> C
|         |          |        |
|   â†—     | Ï†*  â‰…    | Î´* â†—   |
|         v          v        |
C --- >  C/E ---->  C/B ----> C
              P.p
```
-/
def cartesianNatTrans {D F : C} (P : UvPoly R E B) (Q : UvPoly R F D)
    (Î´ : B âŸ¶ D) (Ï† : E âŸ¶ F) (pb : IsPullback P.p Ï† Î´ Q.p) : P.functor âŸ¶ Q.functor :=
  sorry
  -- let cellLeft : TwoSquare (ğŸ­ C) (Over.star F) (Over.star E) (pullback Ï†) :=
  --   (Over.starPullbackIsoStar Ï†).inv
  -- let cellMid :  TwoSquare (pullback Ï†) (pushforward Q.p) (pushforward P.p) (pullback Î´) :=
  --   (pushforwardPullbackIsoSquare pb.flip).inv
  -- let cellRight : TwoSquare (pullback Î´) (forget D) (forget B) (ğŸ­ C) :=
  --   pullbackForgetTwoSquare Î´
  -- let := cellLeft â‰«áµ¥ cellMid â‰«áµ¥ cellRight
  -- this

theorem isCartesian_cartesianNatTrans {D F : C} (P : UvPoly R E B) (Q : UvPoly R F D)
    (Î´ : B âŸ¶ D) (Ï† : E âŸ¶ F) (pb : IsPullback P.p Ï† Î´ Q.p) :
    (cartesianNatTrans P Q Î´ Ï† pb).IsCartesian := by
  sorry
  -- simp [cartesianNatTrans]
  -- infer_instance

  -- (isCartesian_of_isIso _).vComp <|
  -- (isCartesian_of_isIso _).vComp <|
  -- isCartesian_pullbackForgetTwoSquare _

/-- A morphism from a polynomial `P` to a polynomial `Q` is a pair of morphisms `e : E âŸ¶ E'`
and `b : B âŸ¶ B'` such that the diagram
```
      E -- P.p ->  B
      ^            |
   Ï  |            |
      |     Ïˆ      |
      Pb --------> B
      |            |
   Ï†  |            | Î´
      v            v
      F -- Q.p ->  D
```
is a pullback square. -/
structure Hom {F D : C} (P : UvPoly R E B) (Q : UvPoly R F D) where
  Pb : C
  Î´ : B âŸ¶ D
  Ï† : Pb âŸ¶ F
  Ïˆ : Pb âŸ¶ B
  Ï : Pb âŸ¶ E
  is_pb : IsPullback Ïˆ Ï† Î´ Q.p
  w : Ï â‰« P.p = Ïˆ

namespace Hom

open IsPullback

/-- The identity morphism in the category of polynomials. -/
def id (P : UvPoly R E B) : Hom P P := âŸ¨E, ğŸ™ B, ğŸ™ _ , P.p , ğŸ™ _, IsPullback.of_id_snd, by simpâŸ©

end Hom

/-- The domain of the composition of two polynomial signatures.
See `UvPoly.comp`. -/
def compDom {E B E' B' : C} (P : UvPoly R E B) (P' : UvPoly R E' B') : C :=
  Limits.pullback (sndProj P B') P'.p

/--
The composition of two polynomial signatures. See `UvPoly.comp`.
Note that this is not just composition in the category `C`,
instead it is functor composition in the category `C â¥¤ C`,
meaning it satisfies `P.functor â‹™ P'.functor â‰… (comp P P').functor`.

   E' <---- compDom
   |           |
p' |   (pb)    |
   |           |
   V           V
   B' <-----   â€¢ -------> E
      sndProj  |          |
               |   (pb)   |p
               |          |
               V          V
             P @ B' -----> B
                    fstProj
-/
@[simps!]
def comp {E B E' B' : C} (P : UvPoly R E B) (P' : UvPoly R E' B') :
    UvPoly R (compDom P P') (P @ B') where
  p := Limits.pullback.fst (sndProj P B') P'.p â‰« pullback.fst (fstProj P B') P.p
  morphismProperty := R.comp_mem _ _
   (R.of_isPullback (IsPullback.of_hasPullback (sndProj P B') P'.p).flip P'.morphismProperty)
   (R.of_isPullback (IsPullback.of_hasPullback (fstProj P B') P.p).flip P.morphismProperty)

namespace Equiv

variable {P : UvPoly R E B} {Î“ X Y : C}

/-- Convert the morphism `pair` into a morphism in the over category `Over (âŠ¤_ C)` -/
@[simp]
abbrev homMk (pair : Î“ âŸ¶ P @ X) : Over.mk (terminal.from Î“) âŸ¶
    ((toOverTerminal â‹™ MvPoly.functor P.mvPoly).obj X).toComma := Over.homMk pair

/--
A morphism `pair : Î“ âŸ¶ P @ X` is equivalent to a pair of morphisms
`fst : Î“ âŸ¶ B` and `snd : pb âŸ¶ X` in the following diagram
```
    snd
B <---- pb ------> E
        |          |
        |          |p
        |          |
        V          V
        Î“ -------> B
             fst
```
The following API allows users to convert back and forth along this (natural) bijection.
-/
def fst (pair : Î“ âŸ¶ P @ X) : Î“ âŸ¶ B :=
  (MvPoly.Equiv.fst (homMk pair)).hom

lemma fst_eq (pair : Î“ âŸ¶ P @ X) : fst pair = pair â‰« P.fstProj X := by
  aesop_cat

def snd (pair : Î“ âŸ¶ P @ X) : Limits.pullback (fst pair) P.p âŸ¶ X :=
  (MvPoly.Equiv.snd (homMk pair)).left

lemma snd_eq (pair : Î“ âŸ¶ P @ X) : snd pair =
    Limits.pullback.map (fst pair) P.p (P.fstProj X) P.p pair (ğŸ™ E) (ğŸ™ B) (by simp [fst_eq])
    (by simp) â‰« sndProj P X := by
  simpa [Limits.pullback.map] using congrArg CommaMorphism.left (MvPoly.Equiv.snd_eq (homMk pair))

def snd' (pair : Î“ âŸ¶ P @ X) {pb f g} (H : IsPullback (P := pb) f g (fst pair) P.p) : pb âŸ¶ X :=
  H.isoPullback.hom â‰« snd pair

theorem snd_eq_snd' (pair : Î“ âŸ¶ P @ X) : snd pair = snd' pair (.of_hasPullback ..) :=
  by simp [snd']

/-- Convert the morphism `x` into a morphism in the over category `Over (âŠ¤_ C)` -/
@[simp]
abbrev mkAux (b : Î“ âŸ¶ B) (x : pullback b P.p âŸ¶ X) :
    (PolynomialPartialAdjunction.leftAdjoint P.mvPoly.i.fst P.mvPoly.p).obj (Over.mk b) âŸ¶
    ((toOverTerminal (R := R)).obj X).toComma :=
  Over.homMk x

def mk (b : Î“ âŸ¶ B) (x : pullback b P.p âŸ¶ X) : Î“ âŸ¶ P @ X :=
  (MvPoly.Equiv.mk (P := P.mvPoly) (Î“ := Over.mk (terminal.from Î“))
    (Over.mk b) (by congr; apply terminal.hom_ext) (mkAux b x)).left

def mk' (b : Î“ âŸ¶ B) {pb f g} (H : IsPullback (P := pb) f g b P.p) (x : pb âŸ¶ X) : Î“ âŸ¶ P @ X :=
  mk b (H.isoPullback.inv â‰« x)

theorem mk_eq_mk' (b : Î“ âŸ¶ B) (x : pullback b P.p âŸ¶ X) :
    mk b x = mk' b (.of_hasPullback ..) x := by simp [mk']

@[simp]
lemma fst_mk (b : Î“ âŸ¶ B) (x : pullback b P.p âŸ¶ X) :
    fst (mk b x) = b := by
  simp only [fst, mk, Over.homMk_eta]
  rw! (castMode := .all) [MvPoly.Equiv.fst_mk]
  simp [â† heq_eq_eq]; rfl

@[simp]
lemma fst_mk' (b : Î“ âŸ¶ B) {pb f g} (H : IsPullback (P := pb) f g b P.p) (x : pb âŸ¶ X) :
    fst (mk' b H x) = b := by
  simp [mk']

@[simp]
lemma mk'_comp_fstProj (b : Î“ âŸ¶ B) {pb f g} (H : IsPullback (P := pb) f g b P.p) (x : pb âŸ¶ X) :
    mk' b H x â‰« P.fstProj X = b := by
  simp [â† fst_eq]

theorem fst_comp_left (pair : Î“ âŸ¶ P @ X) {Î”} (f : Î” âŸ¶ Î“) :
    fst (f â‰« pair) = f â‰« fst pair := by simp [fst_eq]

theorem fst_comp_right (pair : Î“ âŸ¶ P @ X) (f : X âŸ¶ Y) :
    fst (pair â‰« P.functor.map f) = fst pair := by
  simp [fst_eq]

lemma snd'_eq (pair : Î“ âŸ¶ P @ X) {pb f g} (H : IsPullback (P := pb) f g (fst pair) P.p) :
    snd' pair H = pullback.lift (f â‰« pair) g (by simpa using H.w) â‰« sndProj P X := by
  rw [snd', snd_eq, â† Category.assoc]
  congr 1
  ext <;> simp

/-- Switch the selected pullback `pb` used in `UvPoly.Equiv.snd'` with a different pullback `pb'`. -/
lemma snd'_eq_snd' (pair : Î“ âŸ¶ P @ X) {pb f g} (H : IsPullback (P := pb) f g (fst pair) P.p)
    {pb' f' g'} (H' : IsPullback (P := pb') f' g' (fst pair) P.p) :
    snd' pair H = (H.isoIsPullback _ _ H').hom â‰« snd' pair H' := by
  simp [snd'_eq, â† Category.assoc]
  congr 2
  ext <;> simp

@[simp]
lemma snd_mk (b : Î“ âŸ¶ B) (x : pullback b P.p âŸ¶ X) : snd (mk b x) =
    eqToHom (by simp) â‰« x := by
  have := MvPoly.Equiv.snd_mk (P := P.mvPoly) (Î“ := Over.mk (terminal.from Î“))
    (Over.mk b) (by congr; apply terminal.hom_ext) (mkAux b x)
  convert congr_arg CommaMorphism.left this
  simp

@[simp]
lemma snd'_mk' (b : Î“ âŸ¶ B) {pb f g} (H : IsPullback (P := pb) f g b P.p) (x : pb âŸ¶ X) :
    snd' (mk' b H x) (by rwa [fst_mk']) = x := by
  simp only [snd', mk', snd_mk]
  rw! [fst_mk]
  simp

@[simp]
lemma snd'_mk'' (b : Î“ âŸ¶ B) {pb f g} (H : IsPullback (P := pb) f g b P.p) (x : pb âŸ¶ X)
   {pb' f' g'} (H' : IsPullback (P := pb') f' g' (fst (mk' b H x)) P.p := by exact H) :
    snd' (mk' b H x) H' = H.lift f' g' (by rw [fst_mk'] at H'; simp [H'.w]) â‰« x := by
  simp only [snd', mk', snd_mk]
  rw! [fst_mk]
  simp [â† Category.assoc]
  congr 1
  apply H.hom_ext <;> simp


lemma snd_mk_heq (b : Î“ âŸ¶ B) (x : pullback b P.p âŸ¶ X) :
    snd (mk b x) â‰ x := by
  simp

theorem snd'_comp_left (pair : Î“ âŸ¶ P @ X)
    {pb f g} (H : IsPullback (P := pb) f g (fst pair) P.p)
    {Î”} (Ïƒ : Î” âŸ¶ Î“)
    {pb' f' g'} (H' : IsPullback (P := pb') f' g' (Ïƒ â‰« fst pair) P.p) :
    snd' (Ïƒ â‰« pair) (by convert H'; rw [fst_comp_left]) =
    H.lift (f' â‰« Ïƒ) g' (by simp [H'.w]) â‰« snd' pair H := by
  simp only [snd'_eq, â† Category.assoc]
  congr 2
  ext
  Â· simp
  Â· simp

theorem snd'_comp_right (pair : Î“ âŸ¶ P @ X) (f : X âŸ¶ Y)
    {pb f1 f2} (H : IsPullback (P := pb) f1 f2 (fst pair) P.p) :
    snd' (pair â‰« P.functor.map f) (by rwa [fst_comp_right]) =
    snd' pair H â‰« f := by
  simp only [snd'_eq, assoc]
  conv => right; rw [sndProj_comp, â† Category.assoc]
  congr 1
  ext <;> simp

theorem snd_comp_right (pair : Î“ âŸ¶ P @ X) (f : X âŸ¶ Y) : snd (pair â‰« P.functor.map f) =
    eqToHom (by congr 1; apply fst_comp_right) â‰« snd pair â‰« f := by
  simp only [snd_eq, assoc, sndProj_comp]
  conv => right; simp only [â† Category.assoc]
  congr 1
  have : fst (pair â‰« P.functor.map f) = fst pair := by simp [fst_eq]
  rw! [this]
  ext <;> simp

@[simp]
lemma eta (pair : Î“ âŸ¶ P @ X) :
    mk (fst pair) (snd pair) = pair := by
  have := MvPoly.Equiv.eta (P := P.mvPoly) (Î“ := Over.mk (terminal.from Î“)) (homMk pair)
  exact congr_arg CommaMorphism.left this

@[simp]
lemma eta' (pair : Î“ âŸ¶ P @ X)
    {pb f1 f2} (H : IsPullback (P := pb) f1 f2 (fst pair) P.p) :
    mk' (fst pair) H (snd' pair H) = pair := by
  simp only [mk', snd']
  simp

lemma ext' {pairâ‚ pairâ‚‚ : Î“ âŸ¶ P @ X}
    {pb f g} (H : IsPullback (P := pb) f g (fst pairâ‚) P.p)
    (h1 : fst pairâ‚ = fst pairâ‚‚)
    (h2 : snd' pairâ‚ H = snd' pairâ‚‚ (by rwa [h1] at H)) :
    pairâ‚ = pairâ‚‚ := by
  rw [â† eta' pairâ‚ H, â† eta' pairâ‚‚ (by rwa [h1] at H), h2]
  rw! [h1]

/-- Switch the selected pullback `pb` used in `UvPoly.Equiv.mk'` with a different pullback `pb'`. -/
theorem mk'_eq_mk' (b : Î“ âŸ¶ B) {pb f g} (H : IsPullback (P := pb) f g b P.p) (x : pb âŸ¶ X)
    {pb' f' g'} (H' : IsPullback (P := pb') f' g' b P.p) :
    mk' b H x = mk' b H' ((IsPullback.isoIsPullback _ _ H H').inv â‰« x) := by
  apply ext' (R := R) (f := f) (g := g) (by convert H; simp)
  Â· have : âˆ€ h, H'.lift f g h â‰« (IsPullback.isoIsPullback Î“ E H H').inv = ğŸ™ pb := by
      intro ; apply H.hom_ext <;> simp
    simp [â† Category.assoc, this]
  Â· simp

lemma mk'_comp_right (b : Î“ âŸ¶ B) {pb f1 f2} (H : IsPullback (P := pb) f1 f2 b P.p) (x : pb âŸ¶ X)
    (f : X âŸ¶ Y) : mk' b H x â‰« P.functor.map f = mk' b H (x â‰« f) := by
  refine .symm <| ext' (by rwa [fst_mk']) (by simp [fst_comp_right]) ?_
  rw [snd'_comp_right (H := by rwa [fst_mk'])]; simp

lemma mk_comp_right (b : Î“ âŸ¶ B) (x : pullback b P.p âŸ¶ X) (f : X âŸ¶ Y) :
    mk b x â‰« P.functor.map f = mk b (x â‰« f) := by
  simp [mk_eq_mk', mk'_comp_right]

theorem mk'_comp_left {Î”}
    (b : Î“ âŸ¶ B) {pb f g} (H : IsPullback f g b P.p) (x : pb âŸ¶ X) (Ïƒ : Î” âŸ¶ Î“)
    (Ïƒb) (eq : Ïƒ â‰« b = Ïƒb)
    {pb' f' g'} (H' : IsPullback (P := pb') f' g' Ïƒb P.p) :
    Ïƒ â‰« UvPoly.Equiv.mk' b H x = UvPoly.Equiv.mk' Ïƒb H'
    (H.lift (f' â‰« Ïƒ) g' (by simp [eq, H'.w]) â‰« x) := by
  apply ext' (f := f') (g := g') (H := by convert H'; simp [eq, fst_eq])
  Â· rw [snd'_comp_left (H := by convert H; rw [fst_mk']) (H' := by convert H'; rw [â† eq, fst_mk'])]
    simp
  Â· simp [eq, fst_comp_left]

theorem mk_comp_left {Î”} (b : Î“ âŸ¶ B) (x : pullback b P.p âŸ¶ X) (Ïƒ: Î” âŸ¶ Î“) :
    Ïƒ â‰« UvPoly.Equiv.mk b x =
    UvPoly.Equiv.mk (Ïƒ â‰« b)
      (pullback.map _ _ _ _ Ïƒ (ğŸ™ _) (ğŸ™ _) (by simp) (by simp) â‰« x) := by
  simp only [mk_eq_mk']
  rw [mk'_comp_left (H := .of_hasPullback _ _) (H' := .of_hasPullback _ _) (eq := rfl)]
  congr 2; ext <;> simp

-- lemma mk'_comp_cartesianNatTrans_app {E' B' Î“ X : C} {P' : UvPoly R E' B'}
--     (y : Î“ âŸ¶ B) (pb f g) (H : IsPullback (P := pb) f g y P.p.1)
--     (x : pb âŸ¶ X) (e : E âŸ¶ E') (b : B âŸ¶ B')
--     (hp : IsPullback P.p.1 e b P'.p.1) :
--     Equiv.mk' y H x â‰« (P.cartesianNatTrans P' b e hp).app X =
--     Equiv.mk' P' X (y â‰« b) (H.paste_vert hp) x := by
--   have : fst P' X (Equiv.mk' P X y H x â‰« (P.cartesianNatTrans P' b e hp).app X) = y â‰« b := by
--     rw [fst_eq, Category.assoc, cartesianNatTrans_fstProj, â† Category.assoc, mk'_comp_fstProj]
--   refine ext' _ _ (this â–¸ H.paste_vert hp) (by simpa) ?_
--   simp; rw [snd'_eq]
--   have := snd'_mk' P X y H x
--   rw [snd'_eq, â† fan_snd_map' _ _ X hp] at this
--   refine .trans ?_ this
--   simp only [â† Category.assoc]; congr 1; ext <;> simp

end Equiv

namespace compDomEquiv

variable {Î“ E B E' B' : C} {P : UvPoly R E B} {P' : UvPoly R E' B'}

/-
```
   Î“
   |
   |triple
   V
 compDom
   |âŸ
   |   âŸ
   |      âŸ
   V         â†˜
   â€¢ -------> E
   |          |
   |   (pb)   |p
   |          |
   V          V
P @ B' -----> B
       fstProj
```
This produces a map `fst : Î“ âŸ¶ E`,
and a map `(triple â‰« P.comp P').p : Î“ âŸ¶ P @ B'`,
which we can further break up using `UvPoly.Equiv.fst` and `UvPoly.Equiv.snd`.
```
  dependent
B <---- pb ------> E
        |          |
        |          |p
        |          |
        V          V
        Î“ -------> B
            base
```
-/
def fst (triple : Î“ âŸ¶ compDom P P') : Î“ âŸ¶ E :=
  triple â‰« pullback.fst _ _ â‰« pullback.snd _ _

@[simp]
abbrev base (triple : Î“ âŸ¶ compDom P P') : Î“ âŸ¶ B := Equiv.fst (triple â‰« (P.comp P').p)

theorem fst_comp_p (triple : Î“ âŸ¶ compDom P P') :
    fst triple â‰« P.p = base triple := by
  simp [fst, Equiv.fst_eq, pullback.condition]

abbrev dependent (triple : Î“ âŸ¶ compDom P P') {pb} (f : pb âŸ¶ Î“) (g : pb âŸ¶ E)
    (H : IsPullback f g (fst triple â‰« P.p) P.p) : pb âŸ¶ B' :=
  Equiv.snd' (triple â‰« (P.comp P').p) (by convert H; simp only [fst_comp_p])

def snd (triple : Î“ âŸ¶ compDom P P') : Î“ âŸ¶ E' :=
  triple â‰« pullback.snd _ _

theorem snd_comp_p (triple : Î“ âŸ¶ compDom P P')
    {pb} (f : pb âŸ¶ Î“) (g : pb âŸ¶ E) (H : IsPullback f g (fst triple â‰« P.p) P.p) :
    snd triple â‰« P'.p =
    H.lift (ğŸ™ Î“) (fst triple) (by simp) â‰« dependent triple f g H :=
  calc (triple â‰« pullback.snd _ _) â‰« P'.p
  _ = triple â‰« pullback.fst _ _ â‰« sndProj P B' := by
    simp [pullback.condition]
  _ = H.lift (ğŸ™ Î“) (fst triple) (by simp) â‰« dependent triple f g H := by
    simp only [â† assoc, dependent, comp_p, Equiv.snd'_eq]
    congr 1
    ext <;> simp [fst]

def mk (b : Î“ âŸ¶ B) (e : Î“ âŸ¶ E) (he : e â‰« P.p = b)
    {pb} (f : pb âŸ¶ Î“) (g : pb âŸ¶ E) (H : IsPullback f g b P.p)
    (b' : pb âŸ¶ B') (e' : Î“ âŸ¶ E') (he' : e' â‰« P'.p = H.lift (ğŸ™ Î“) e (by simp [he]) â‰« b') :
    Î“ âŸ¶ P.compDom P' :=
  pullback.lift (pullback.lift (Equiv.mk' b H b') e) e' (by
    have : b' = Equiv.snd' (Equiv.mk' b H b') (by convert H; simp) := by rw [Equiv.snd'_mk']
    conv => right; rw [he', this, Equiv.snd'_eq, â† Category.assoc]
    congr 1
    ext <;> simp )

@[simp]
lemma base_mk (b : Î“ âŸ¶ B) (e : Î“ âŸ¶ E) (he : e â‰« P.p = b)
    {pb} (f : pb âŸ¶ Î“) (g : pb âŸ¶ E) (H : IsPullback f g b P.p)
    (b' : pb âŸ¶ B') (e' : Î“ âŸ¶ E') (he' : e' â‰« P'.p = H.lift (ğŸ™ Î“) e (by simp [he]) â‰« b') :
  base (mk b e he f g H b' e' he') = b := by simp [mk]

@[simp]
lemma fst_mk (b : Î“ âŸ¶ B) (e : Î“ âŸ¶ E) (he : e â‰« P.p = b)
    {pb} (f : pb âŸ¶ Î“) (g : pb âŸ¶ E) (H : IsPullback f g b P.p)
    (b' : pb âŸ¶ B') (e' : Î“ âŸ¶ E') (he' : e' â‰« P'.p = H.lift (ğŸ™ Î“) e (by simp [he]) â‰« b') :
  fst (mk b e he f g H b' e' he') = e := by
  simp [mk, fst]

@[simp]
lemma dependent_mk (b : Î“ âŸ¶ B) (e : Î“ âŸ¶ E) (he : e â‰« P.p = b)
    {pb} (f : pb âŸ¶ Î“) (g : pb âŸ¶ E) (H : IsPullback f g b P.p)
    (b' : pb âŸ¶ B') (e' : Î“ âŸ¶ E') (he' : e' â‰« P'.p = H.lift (ğŸ™ Î“) e (by simp [he]) â‰« b')
    {pb'} (f' : pb' âŸ¶ Î“) (g' : pb' âŸ¶ E)
    (H' : IsPullback f' g' (fst (mk b e he f g H b' e' he') â‰« P.p) P.p) :
  dependent (mk b e he f g H b' e' he') f' g' H' = H.lift f' g' (by simp [â† H'.w, he]) â‰« b' := by
  simp [mk, dependent]

@[simp]
lemma snd_mk (b : Î“ âŸ¶ B) (e : Î“ âŸ¶ E) (he : e â‰« P.p = b)
    {pb} (f : pb âŸ¶ Î“) (g : pb âŸ¶ E) (H : IsPullback f g b P.p)
    (b' : pb âŸ¶ B') (e' : Î“ âŸ¶ E') (he' : e' â‰« P'.p = H.lift (ğŸ™ Î“) e (by simp [he]) â‰« b') :
  snd (mk b e he f g H b' e' he') = e' := by
  simp [mk, snd]

@[simp]
lemma eta (triple : Î“ âŸ¶ compDom P P') {pb} (f : pb âŸ¶ Î“) (g : pb âŸ¶ E)
    (H : IsPullback f g (base triple) P.p) (b' : pb âŸ¶ B')
    (hbase' : b' = Equiv.snd' (triple â‰« (P.comp P').p) H) :
    mk (base triple) (fst triple) (fst_comp_p ..) f g H b' (snd triple) (by
      simp only [snd, assoc, â† pullback.condition, base, comp_p]
      simp only [hbase', Equiv.snd'_eq, â† Category.assoc]
      congr 1
      ext <;> simp [fst]) = triple := by
  apply pullback.hom_ext
  Â· ext
    Â· simp [mk]
      conv => right; rw [â† Equiv.eta'
        (triple â‰« pullback.fst (P.sndProj B') P'.p â‰« pullback.fst (P.fstProj B') P.p) H]
      congr
    Â· simp [mk, fst]
  Â· simp [mk, snd]

lemma ext (triple triple' : Î“ âŸ¶ compDom P P')
    (hfst : fst triple = fst triple')
    (hsnd : snd triple = snd triple')
    {pb} (f : pb âŸ¶ Î“) (g : pb âŸ¶ E)
    (H : IsPullback f g (fst triple â‰« P.p) P.p)
    (hd : dependent triple f g H = dependent triple' f g (by rwa [â† hfst])) :
    triple = triple' := by
  rw [â† eta triple f g (by convert H; simp [fst_comp_p]) (dependent triple f g H) rfl,
    â† eta triple' f g (by rwa [â† fst_comp_p, â† hfst])
    (dependent triple' f g (by rwa [â† hfst])) rfl]
  have : base triple = base triple' := by
    rw [â† fst_comp_p, â† fst_comp_p, hfst]
  rw! [hsnd, hd, hfst, this]

lemma fst_comp {Î”} (Ïƒ : Î” âŸ¶ Î“) (triple : Î“ âŸ¶ compDom P P') :
    fst (Ïƒ â‰« triple) = Ïƒ â‰« fst triple := by
  simp [fst]

lemma snd_comp {Î”} (Ïƒ : Î” âŸ¶ Î“) (triple : Î“ âŸ¶ compDom P P') :
    snd (Ïƒ â‰« triple) = Ïƒ â‰« snd triple := by
  simp [snd]

lemma dependent_comp {Î”} (Ïƒ : Î” âŸ¶ Î“) (triple : Î“ âŸ¶ compDom P P')
    {pb'} (f' : pb' âŸ¶ Î“) (g' : pb' âŸ¶ E) (H' : IsPullback f' g' (fst triple â‰« P.p) P.p)
    {pb} (f : pb âŸ¶ Î”) (g : pb âŸ¶ E) (H : IsPullback f g (fst (Ïƒ â‰« triple) â‰« P.p) P.p) :
    dependent (Ïƒ â‰« triple) f g H = H'.lift (f â‰« Ïƒ) g (by simp [â† H.w, fst_comp]) â‰«
    dependent triple f' g' H' := by
  simp only [dependent, comp_p, â† assoc, Equiv.snd'_eq]
  congr
  ext <;> simp

lemma comp_mk {Î”} (Ïƒ : Î” âŸ¶ Î“) (b : Î“ âŸ¶ B) (e : Î“ âŸ¶ E) (he : e â‰« P.p = b)
    {pb'} (f' : pb' âŸ¶ Î“) (g' : pb' âŸ¶ E) (H' : IsPullback f' g' b P.p)
    {pb} (f : pb âŸ¶ Î”) (g : pb âŸ¶ E) (H : IsPullback f g (Ïƒ â‰« b) P.p)
    (b' : pb' âŸ¶ B') (e' : Î“ âŸ¶ E') (he' : e' â‰« P'.p = H'.lift (ğŸ™ Î“) e (by simp [he]) â‰« b') :
    Ïƒ â‰« mk b e he f' g' H' b' e' he' =
    mk (Ïƒ â‰« b) (Ïƒ â‰« e) (by simp [he]) f g H (H'.lift (f â‰« Ïƒ) g (by simp[â† H.w]) â‰« b') (Ïƒ â‰« e')
    (by simp [he']; simp [â† assoc]; congr 1; apply H'.hom_ext <;> simp) := by
  simp [mk]
  apply pullback.hom_ext
  Â· apply pullback.hom_ext
    Â· simp only [assoc, limit.lift_Ï€, PullbackCone.mk_pt, PullbackCone.mk_Ï€_app]
      rw [Equiv.mk'_comp_left]
      rfl
    Â· simp
  Â· simp

end compDomEquiv

instance preservesPullbacks (P : UvPoly R E B) {Pb X Y Z : C} (fst : Pb âŸ¶ X) (snd : Pb âŸ¶ Y)
    (f : X âŸ¶ Z) (g : Y âŸ¶ Z) (h: IsPullback fst snd f g) :
    IsPullback (P.functor.map fst) (P.functor.map snd) (P.functor.map f) (P.functor.map g) :=
  P.functor.map_isPullback h
