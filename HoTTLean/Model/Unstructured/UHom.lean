import Mathlib.CategoryTheory.Limits.Shapes.StrictInitial
import HoTTLean.ForMathlib
import HoTTLean.Model.Unstructured.UnstructuredUniverse

/-! Morphisms of unstructured models, and Russell-universe embeddings. -/

universe v u

noncomputable section

open CategoryTheory Opposite MonoidalCategory

namespace Model

namespace UnstructuredUniverse

variable {Ctx : Type u} [Category Ctx] (M : UnstructuredUniverse Ctx)

open ChosenTerminal

macro "by>" s:tacticSeq : term => `(by as_aux_lemma => $s)

structure Hom (M N : UnstructuredUniverse Ctx) where
  mapTm : M.Tm âŸ¶ N.Tm
  mapTy : M.Ty âŸ¶ N.Ty
  pb : IsPullback mapTm M.tp N.tp mapTy

def Hom.id (M : UnstructuredUniverse Ctx) : Hom M M where
  mapTm := ğŸ™ _
  mapTy := ğŸ™ _
  pb := IsPullback.of_id_fst

def Hom.comp {M N O : UnstructuredUniverse Ctx} (Î± : Hom M N) (Î² : Hom N O) : Hom M O where
  mapTm := Î±.mapTm â‰« Î².mapTm
  mapTy := Î±.mapTy â‰« Î².mapTy
  pb := Î±.pb.paste_horiz Î².pb

def Hom.comp_assoc {M N O P : UnstructuredUniverse Ctx} (Î± : Hom M N) (Î² : Hom N O) (Î³ : Hom O P) :
    comp (comp Î± Î²) Î³ = comp Î± (comp Î² Î³) := by
  simp [comp]

/-- Morphism into the representable natural transformation `M`
from the pullback of `M` along a type. -/
protected def pullbackHom (M : UnstructuredUniverse Ctx) {Î“ : Ctx} (A : (Î“) âŸ¶ M.Ty) :
    Hom (M.pullback A) M where
  mapTm := M.var A
  mapTy := A
  pb := M.disp_pullback A

/-- Given `M : Universe`, a semantic type `A : (Î“) âŸ¶ M.Ty`,
and a substitution `Ïƒ : Î” âŸ¶ Î“`, construct a Hom for the substitution `A[Ïƒ]`.
-/
def Hom.subst (M : UnstructuredUniverse Ctx)
    {Î“ Î” : Ctx} (A : (Î“) âŸ¶ M.Ty) (Ïƒ : Î” âŸ¶ Î“) :
    Hom (M.pullback ((Ïƒ) â‰« A)) (M.pullback A) :=
  let AÏƒ := (Ïƒ) â‰« A
  { mapTm :=
    (M.disp_pullback A).lift (M.var AÏƒ) (M.disp AÏƒ â‰« Ïƒ) (by aesop_cat)
    mapTy := (Ïƒ)
    pb := by
      convert IsPullback.of_right' (M.disp_pullback AÏƒ) (M.disp_pullback A)}

@[simp] def Hom.extIsoExt {M N : UnstructuredUniverse Ctx} (h : Hom M N)
    {Î“} (A : Î“ âŸ¶ M.Ty) : (N.ext (A â‰« h.mapTy)) â‰… (M.ext A) :=
  IsPullback.isoIsPullback N.Tm Î“ (N.disp_pullback (A â‰« h.mapTy))
  (IsPullback.paste_horiz (M.disp_pullback A) h.pb)

variable [ChosenTerminal Ctx]

/-- A Russell universe embedding is a hom of natural models `M âŸ¶ N`
such that types in `M` correspond to terms of a universe `U` in `N`.

These don't form a category since `UHom.id M` is essentially `Type : Type` in `M`.

Note this doesn't need to extend `Hom` as none of its fields are used;
it's just convenient to pack up the data. -/
structure UHom (M N : UnstructuredUniverse Ctx) extends Hom M N where
  U : terminal âŸ¶ N.Ty
  asTm : M.Ty âŸ¶ N.Tm
  U_pb : IsPullback
            /- m.Ty -/           asTm /- N.Tm -/
    (isTerminal.from M.Ty)         N.tp
             /- âŠ¤ -/               U  /- N.Ty -/

def UHom.ofTyIsoExt
    {M N : UnstructuredUniverse Ctx}
    (H : Hom M N) {U : (ğŸ­_ Ctx) âŸ¶ N.Ty} (i : M.Ty â‰… (N.ext U)) :
    UHom M N where
  __ := H
  U := U
  asTm := i.hom â‰« N.var U
  U_pb := by
    convert IsPullback.of_iso_isPullback (N.disp_pullback _) i
    apply isTerminal.hom_ext

def UHom.comp {M N O : UnstructuredUniverse Ctx} (Î± : UHom M N) (Î² : UHom N O) : UHom M O where
  __ := Hom.comp Î±.toHom Î².toHom
  U := Î±.U â‰« Î².mapTy
  asTm := Î±.asTm â‰« Î².mapTm
  U_pb := Î±.U_pb.paste_horiz Î².pb

def UHom.comp_assoc {M N O P : UnstructuredUniverse Ctx} (Î± : UHom M N) (Î² : UHom N O) (Î³ : UHom O P) :
    comp (comp Î± Î²) Î³ = comp Î± (comp Î² Î³) := by
  simp [comp, Hom.comp]

def UHom.wkU {M N : UnstructuredUniverse Ctx} (Î“ : Ctx) (Î± : UHom M N) : (Î“) âŸ¶ N.Ty :=
  isTerminal.from Î“ â‰« Î±.U

@[reassoc (attr := simp)]
theorem UHom.comp_wkU {M N : UnstructuredUniverse Ctx} {Î” Î“ : Ctx} (Î± : UHom M N) (f : (Î”) âŸ¶ (Î“)) :
    f â‰« Î±.wkU Î“ = Î±.wkU Î” := by
  simp [wkU]

/- Sanity check:
construct a `UHom` into a natural model with a Tarski universe. -/
def UHom.ofTarskiU (M : UnstructuredUniverse Ctx) (U : (ğŸ­_ Ctx) âŸ¶ M.Ty) (El : (M.ext U) âŸ¶ M.Ty) :
    UHom (M.pullback El) M where
  __ := M.pullbackHom El
  U
  asTm := M.var U
  U_pb := (M.disp_pullback U).of_iso
      (Iso.refl _)
      (Iso.refl _)
      (Iso.refl _)
      (Iso.refl _)
      (by simp) (isTerminal.hom_ext ..)
      (by simp) (by simp)

section

variable {M N : UnstructuredUniverse Ctx} (Î± : Hom M N) {Î“ Î” : Ctx} {Ïƒ : Î” âŸ¶ Î“} (A : Î“ âŸ¶ M.Ty)
  {ÏƒA : Î” âŸ¶ M.Ty} (eq : Ïƒ â‰« A = ÏƒA)

def Hom.extCompMapTyIsoExt : N.ext (A â‰« Î±.mapTy) â‰… M.ext A :=
  (N.disp_pullback (A â‰« Î±.mapTy)).isoIsPullback _ _
  (IsPullback.paste_horiz (M.disp_pullback A) Î±.pb)

lemma Hom.extCompMapTyIsoExt_hom_comp_substWk  :
    (Î±.extCompMapTyIsoExt ÏƒA).hom â‰« M.substWk Ïƒ A ÏƒA eq =
    N.substWk Ïƒ (A â‰« Î±.mapTy) (ÏƒA â‰« Î±.mapTy) (by simp [â† eq]) â‰« (Î±.extCompMapTyIsoExt A).hom := by
  apply (disp_pullback ..).hom_ext
  Â· apply Î±.pb.hom_ext
    Â· simp [extCompMapTyIsoExt]
    Â· simp [eq, extCompMapTyIsoExt]
  Â· simp [extCompMapTyIsoExt]

end

namespace PolymorphicPi.ofMonomorphic

variable {U0 U1 U2 : UnstructuredUniverse Ctx} (P : PolymorphicPi U2 U2 U2)
    (l0 : Hom U0 U2) (l1 : Hom U1 U2) {Î“ : Ctx} {A : Î“ âŸ¶ U0.Ty}
    (B : U0.ext A âŸ¶ U1.Ty)

abbrev B2 := (l0.extCompMapTyIsoExt A).hom â‰« B â‰« l1.mapTy

def Pi : Î“ âŸ¶ U2.Ty := P.Pi (B2 l0 l1 B)

def lam (b : U0.ext A âŸ¶ U1.Tm) (b_tp : b â‰« U1.tp = B) : Î“ âŸ¶ U2.Tm :=
  P.lam ((l0.extCompMapTyIsoExt A).hom â‰« B â‰« l1.mapTy)
  ((l0.extCompMapTyIsoExt A).hom â‰« b â‰« l1.mapTm) (by simp [â† b_tp, l1.pb.w])

def unLam (f : Î“ âŸ¶ U2.Tm) (hf : f â‰« U2.tp = ofMonomorphic.Pi P l0 l1 B) : U0.ext A âŸ¶ U1.Tm :=
  l1.pb.lift ((Hom.extCompMapTyIsoExt l0 A).inv â‰« P.unLam (B2 l0 l1 B) f (by simp [hf, Pi])) B
  (by simp [P.unLam_tp])

end PolymorphicPi.ofMonomorphic

def PolymorphicPi.ofMonomorphic {U0 U1 U2 : UnstructuredUniverse Ctx} (P : PolymorphicPi U2 U2 U2)
    (l0 : Hom U0 U2) (l1 : Hom U1 U2) : PolymorphicPi U0 U1 U2 where
  Pi := ofMonomorphic.Pi P l0 l1
  Pi_comp Ïƒ A ÏƒA eq B := by
    dsimp [ofMonomorphic.Pi]
    rw [â† P.Pi_comp _ _ (ÏƒA := ÏƒA â‰« l0.mapTy) (by simp [â† eq])]
    simp only [â† Category.assoc, Hom.extCompMapTyIsoExt_hom_comp_substWk]
  lam := ofMonomorphic.lam P l0 l1
  lam_comp Ïƒ A ÏƒA eq B b b_tp := by
    dsimp [ofMonomorphic.lam]
    rw [â† P.lam_comp _ (ÏƒA := ÏƒA â‰« l0.mapTy) (by simp [â† eq])]
    simp only [â† Category.assoc, Hom.extCompMapTyIsoExt_hom_comp_substWk]
  lam_tp _ _ _ := P.lam_tp ..
  unLam := ofMonomorphic.unLam  P l0 l1
  unLam_tp B f f_tp := by simp [ofMonomorphic.unLam]
  unLam_lam B b b_tp := by
    apply l1.pb.hom_ext <;> simp [ofMonomorphic.unLam, ofMonomorphic.lam, P.unLam_lam, b_tp]
  lam_unLam := by simp [ofMonomorphic.unLam, ofMonomorphic.lam, P.lam_unLam]

/-! ## Universe embeddings -/

variable (Ctx) in
/-- A sequence of Russell universe embeddings. -/
structure UHomSeq where
  /-- Number of embeddings in the sequence,
  or one less than the number of models in the sequence. -/
  length : Nat
  objs (i : Nat) (h : i < length + 1) : UnstructuredUniverse Ctx
  homSucc' (i : Nat) (h : i < length) : UHom (objs i <| by omega) (objs (i + 1) <| by omega)

namespace UHomSeq

variable (s : UHomSeq Ctx)

instance : GetElem (UHomSeq Ctx) Nat (UnstructuredUniverse Ctx) (fun s i => i < s.length + 1) where
  getElem s i h := s.objs i h

def homSucc (i : Nat) (h : i < s.length := by get_elem_tactic) : UHom s[i] s[i+1] :=
  s.homSucc' i h

/-- Composition of embeddings between `i` and `j` in the chain. -/
def hom (s : UHomSeq Ctx) (i j : Nat) (ij : i < j := by omega)
    (jlen : j < s.length + 1 := by get_elem_tactic) : UHom s[i] s[j] :=
  if h : i + 1 = j then
    h â–¸ s.homSucc i
  else
    (s.homSucc i).comp <| s.hom (i+1) j
termination_by s.length - i

theorem hom_comp_trans (s : UHomSeq Ctx) (i j k : Nat) (ij : i < j) (jk : j < k)
    (klen : k < s.length + 1) :
    (s.hom i j ij).comp (s.hom j k jk) = s.hom i k (ij.trans jk) := by
  conv_rhs => unfold hom
  conv in s.hom i j _ => unfold hom
  split_ifs
  all_goals try omega
  . rename_i h _
    cases h
    simp
  . rw [UHom.comp_assoc, hom_comp_trans]
termination_by s.length - i

/- It is useful to be able to talk about the underlying sequence of Homs in a UHomSeq.
  For such a sequence, we can loosen the condition i < j to i <= j
  without creating Type in Type.
  This is helpful for defining `s[i] â†’ s[max i j]` for Pi and Sigma below.
-/
def homOfLe (i j : Nat) (ij : i <= j := by omega)
    (jlen : j < s.length + 1 := by get_elem_tactic) : Hom s[i] s[j] :=
  if h : i = j then h â–¸ Hom.id s[i]
  else
    (s.hom i j (by omega) _).toHom

/-- For `i â‰¤ j` and a term `t` at level `j`,
if the type of `t` is lifted from level `i`,
then `t` is a lift of a term at level `i`. -/
def unlift (i j) (ij : i â‰¤ j := by omega) (jlen : j < s.length + 1 := by get_elem_tactic)
    {Î“} (A : Î“ âŸ¶ (s[i]'(ij.trans_lt jlen)).Ty) (t : Î“ âŸ¶ s[j].Tm)
    (eq : t â‰« s[j].tp = A â‰« (s.homOfLe i j).mapTy) :
    Î“ âŸ¶ (s[i]'(ij.trans_lt jlen)).Tm :=
  (s.homOfLe i j).pb.lift t A eq

@[simp]
theorem unlift_tp {i j ij jlen Î“ A}
    {t : (Î“) âŸ¶ _} (eq : t â‰« s[j].tp = A â‰« (s.homOfLe i j).mapTy) :
    s.unlift i j ij jlen A t eq â‰« (s[i]'(ij.trans_lt jlen)).tp = A := by
  simp [unlift]

@[simp]
theorem unlift_lift {i j ij jlen Î“ A}
    {t : (Î“) âŸ¶ _} (eq : t â‰« s[j].tp = A â‰« (s.homOfLe i j).mapTy) :
    s.unlift i j ij jlen A t eq â‰« (s.homOfLe i j).mapTm = t := by
  simp [unlift]

def unliftVar (i j) (ij : i â‰¤ j := by omega) (jlen : j < s.length + 1 := by get_elem_tactic)
    {Î“} (A : (Î“) âŸ¶ (s[i]'(ij.trans_lt jlen)).Ty)
    {A' : (Î“) âŸ¶ s[j].Ty} (eq : A â‰« (s.homOfLe i j).mapTy = A') :
    (s[j].ext A') âŸ¶ (s[i]'(ij.trans_lt jlen)).Tm :=
  s.unlift i j ij jlen ((s[j].disp _) â‰« A) (s[j].var _) (by simp [eq])

@[simp]
theorem unliftVar_tp {i j ij jlen Î“ A} {A' : (Î“) âŸ¶ s[j].Ty}
    (eq : A â‰« (s.homOfLe i j).mapTy = A') :
    s.unliftVar i j ij jlen A eq â‰« (s[i]'(ij.trans_lt jlen)).tp = (s[j].disp _) â‰« A := by
  simp [unliftVar]

theorem substCons_unliftVar {i j ij jlen Î“ A} {A' : (Î“) âŸ¶ s[j].Ty}
    {eq : A â‰« (s.homOfLe i j).mapTy = A'}
    {Î”} {Ïƒ : Î” âŸ¶ Î“} {t : (Î”) âŸ¶ _}
    (t_tp : t â‰« (s[i]'(ij.trans_lt jlen)).tp = (Ïƒ) â‰« A) :
    (s[j].substCons Ïƒ A' (t â‰« (s.homOfLe i j ij jlen).mapTm) (by
      simp [*]
      conv_lhs => rhs; apply (s.homOfLe i j).pb.w
      subst A'; rw [â† Category.assoc, â† Category.assoc, â† t_tp]))
    â‰« s.unliftVar i j ij jlen A eq = t := by
  simp [unlift, unliftVar]; apply (s.homOfLe i j).pb.hom_ext <;> simp [*]

/--
TODO: Consider generalising to just UHom?
Convert a map into the `i`th type classifier into a a term of the
`i+1`th term classifier, that is a term of the `i`th universe.
It is defined by composition with the first projection of the pullback square
               v
     s[i].Ty ----> s[i+1].Tm
     ^    |          |
  A /     |   p.b.   |
   /      |          |
  /       V          V
(Î“) ---> 1 -----> s[i+1].Ty
              U_i
-/
def code {Î“ : Ctx} {i : Nat} (s : UHomSeq Ctx) (ilen : i < s.length) (A : (Î“) âŸ¶ s[i].Ty) :
    (Î“) âŸ¶ s[i+1].Tm :=
  A â‰« (s.homSucc i).asTm

@[simp]
theorem code_tp {Î“ : Ctx} {i : Nat} (s : UHomSeq Ctx) (ilen : i < s.length) (A : (Î“) âŸ¶ s[i].Ty) :
    s.code ilen A â‰« s[i+1].tp = (s.homSucc i).wkU Î“ := by
  simp [code, (s.homSucc i).U_pb.w, UHom.wkU]

@[reassoc]
theorem comp_code {Î” Î“ : Ctx} {i : Nat} (s : UHomSeq Ctx) (ilen : i < s.length)
    (Ïƒ : (Î”) âŸ¶ (Î“)) (A : (Î“) âŸ¶ s[i].Ty) :
    Ïƒ â‰« s.code ilen A = s.code ilen (Ïƒ â‰« A) := by
  simp [code]

/--
TODO: Consider generalising to just UHom?
Convert a a term of the `i`th universe (it is a `i+1` level term) into
a map into the `i`th type classifier.
It is the unique map into the pullback
             a
(Î“) -----------------Â¬
â€–  -->          v     V
â€–    s[i].Ty ----> s[i+1].Tm
â€–         |          |
â€–         |   p.b.   |
â€–         |          |
â€–         V          V
(Î“) ---> 1 -----> s[i+1].Ty
              U_i
-/
def el (s : UHomSeq Ctx) {Î“ : Ctx} {i : Nat} (ilen : i < s.length)
    (a : (Î“) âŸ¶ s[i+1].Tm) (a_tp : a â‰« s[i+1].tp = (s.homSucc i).wkU Î“) :
    (Î“) âŸ¶ s[i].Ty :=
  (s.homSucc i).U_pb.lift a (isTerminal.from (Î“)) (by rw [a_tp, UHom.wkU])

@[reassoc]
theorem comp_el (s : UHomSeq Ctx) {Î” Î“ : Ctx} {i : Nat} (ilen : i < s.length)
    (Ïƒ : (Î”) âŸ¶ (Î“)) (a : (Î“) âŸ¶ s[i+1].Tm) (a_tp : a â‰« s[i+1].tp = (s.homSucc i).wkU Î“) :
    Ïƒ â‰« s.el ilen a a_tp = s.el ilen (Ïƒ â‰« a) (by simp [a_tp]) :=
  (s.homSucc i).U_pb.hom_ext (by simp [el]) (by simp)

@[simp]
lemma el_code {Î“ : Ctx} {i : Nat} (s : UHomSeq Ctx) (ilen : i < s.length) (A : (Î“) âŸ¶ s[i].Ty) :
    el s ilen (code s ilen A) (code_tp _ _ _) = A :=
  (s.homSucc i).U_pb.hom_ext (by simp [el, code]) (by simp)

@[simp]
lemma code_el (s : UHomSeq Ctx) {Î“ : Ctx} {i : Nat} (ilen : i < s.length)
    (a : (Î“) âŸ¶ s[i+1].Tm) (a_tp : a â‰« s[i+1].tp = (s.homSucc i).wkU Î“) :
    code s ilen (el s ilen a a_tp) = a := by
  simp [code, el]

/-! ## Pi -/

/-- `Pi` and `lam` formers at the `max` of any two universes.
This interprets
```
Î“ âŠ¢áµ¢ A type  Î“.A âŠ¢â±¼ B type
--------------------------
Î“ âŠ¢â‚˜â‚â‚“â‚áµ¢,â±¼â‚ Î A. B type
``` -/
class PiSeq (s : UHomSeq Ctx) where
  polyPi (s) (i j : Nat)
    -- Sadly, we have to spell out `ilen` and `jlen` due to
    -- https://leanprover.zulipchat.com/#narrow/stream/270676-lean4/topic/Optional.20implicit.20argument
    (ilen : i < s.length + 1 := by get_elem_tactic)
    (jlen : j < s.length + 1 := by get_elem_tactic) :
    PolymorphicPi s[i] s[j] s[max i j]

-- Re-export for use with dot notation.
abbrev polyPi := @PiSeq.polyPi

def PiSeq.ofMonomorphic
    (nmPi : (i : Nat) â†’ (ilen : i < s.length + 1 := by get_elem_tactic) â†’
    s[i].PolymorphicPi s[i] s[i]) : PiSeq s where
  polyPi i j _ _ := .ofMonomorphic (nmPi (max i j)) (s.homOfLe _ _) (s.homOfLe _ _)

/-! ## Sigma -/

/-- `Sig` and `pair` formers at the `max` of any two universes. -/
class SigSeq (s : UHomSeq Ctx) where
  polySig (s) (i j : Nat)
    (ilen : i < s.length + 1 := by get_elem_tactic)
    (jlen : j < s.length + 1 := by get_elem_tactic) :
    PolymorphicSigma s[i] s[j] s[max i j]

abbrev polySig := @SigSeq.polySig

/-! ## Identity types -/

/-- `Id` and `refl` formers at any universe,
together with identity elimination into any other universe. -/
class IdSeq (s : UHomSeq Ctx) where
  idIntro (s) (i : Nat) (ilen : i < s.length + 1 := by get_elem_tactic) :
    PolymorphicIdIntro s[i] s[i]
  idElim (s) (i j : Nat)
    (ilen : i < s.length + 1 := by get_elem_tactic)
    (jlen : j < s.length + 1 := by get_elem_tactic) :
    PolymorphicIdElim (idIntro i) s[j]

abbrev idIntro := @IdSeq.idIntro
abbrev idElim := @IdSeq.idElim
