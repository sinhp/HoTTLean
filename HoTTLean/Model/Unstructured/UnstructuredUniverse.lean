import Mathlib.CategoryTheory.Limits.Shapes.KernelPair
import HoTTLean.ForMathlib
import HoTTLean.ForMathlib.Tactic.CategoryTheory.FunctorMap
import HoTTLean.ForMathlib.CategoryTheory.RepPullbackCone
import HoTTLean.ForMathlib.CategoryTheory.WeakPullback
import Mathlib.Tactic.DepRewrite

universe v u

noncomputable section

open CategoryTheory Limits Opposite

namespace Model

/-- A natural model with support for dependent types (and nothing more).
The data is a natural transformation with representable fibers,
stored as a choice of representative for each fiber. -/
structure UnstructuredUniverse (Ctx : Type u) [Category Ctx] where
  Tm : Ctx
  Ty : Ctx
  tp : Tm âŸ¶ Ty
  ext {Î“ : Ctx} (A : Î“ âŸ¶ Ty) : Ctx
  disp {Î“ : Ctx} (A : Î“ âŸ¶ Ty) : ext A âŸ¶ Î“
  var {Î“ : Ctx} (A : Î“ âŸ¶ Ty) : ext A âŸ¶ Tm
  disp_pullback {Î“ : Ctx} (A : Î“ âŸ¶ Ty) :
    IsPullback (var A) (disp A) tp A

namespace UnstructuredUniverse

variable {Ctx : Type u} [Category Ctx] (M : UnstructuredUniverse Ctx)

@[reassoc (attr := simp)]
theorem var_tp {Î“ : Ctx} (A : Î“ âŸ¶ M.Ty) : M.var A â‰« M.tp = (M.disp A) â‰« A := by
  simp [(M.disp_pullback A).w]

/-! ## Pullback of representable natural transformation -/

/-- Pull a natural model back along a type. -/
protected def pullback {Î“ : Ctx} (A : Î“ âŸ¶ M.Ty) : UnstructuredUniverse Ctx where
  Tm := M.ext A
  Ty := Î“
  tp := M.disp A
  ext := fun B => M.ext (B â‰« A)
  disp := fun B => M.disp (B â‰« A)
  var := fun B => (M.disp_pullback A).lift (M.var (B â‰« A))
    (M.disp (B â‰« A) â‰« B) (by simp [(M.disp_pullback (B â‰« A)).w])
  disp_pullback := fun B =>
    IsPullback.of_right' (M.disp_pullback (B â‰« A)) (M.disp_pullback A)

/--
  Given the pullback square on the right,
  with a natural model structure on `tp : Tm âŸ¶ Ty`
  giving the outer pullback square.

  Î“.A -.-.- var -.-,-> E ------ toTm ------> Tm
   |                   |                      |
   |                   |                      |
 M.disp                Ï€                     tp
   |                   |                      |
   V                   V                      V
  Î“ ------- A -------> U ------ toTy ------> Ty

  construct a natural model structure on `Ï€ : E âŸ¶ U`,
  by pullback pasting.
-/
def ofIsPullback {U E : Ctx} {Ï€ : E âŸ¶ U}
    {toTy : U âŸ¶ M.Ty} {toTm : E âŸ¶ M.Tm}
    (pb : IsPullback toTm Ï€ M.tp toTy) :
    UnstructuredUniverse Ctx where
  Ty := U
  Tm := E
  tp := Ï€
  ext A := M.ext (A â‰« toTy)
  disp A := M.disp (A â‰« toTy)
  var A := pb.lift (M.var (A â‰« toTy)) (M.disp (A â‰« toTy) â‰« A)
    (by simp [(M.disp_pullback (A â‰« toTy)).w])
  disp_pullback A := IsPullback.of_right' (M.disp_pullback (A â‰« toTy)) pb

/-! ## Substitutions -/

/--
```
Î” âŠ¢ Ïƒ : Î“  Î“ âŠ¢ A type  Î” âŠ¢ t : A[Ïƒ]
-----------------------------------
Î” âŠ¢ Ïƒ.t : Î“.A
```
 ------ Î” ------ t --------Â¬
 |      â†“ substCons         â†“
 |   M.ext A ---var A---> M.Tm
 |      |                  |
 Ïƒ      |                  |
 |    disp A              M.tp
 |      |                  |
 |      V                  V
  ---> Î“ ------ A -----> M.Ty
-/
def substCons {Î” Î“ : Ctx} (Ïƒ : Î” âŸ¶ Î“) (A : Î“ âŸ¶ M.Ty)
    (t : Î” âŸ¶ M.Tm) (t_tp : t â‰« M.tp = Ïƒ â‰« A) :
    Î” âŸ¶ M.ext A :=
  (M.disp_pullback A).lift t Ïƒ t_tp

@[reassoc (attr := simp)]
theorem substCons_disp {Î” Î“ : Ctx} (Ïƒ : Î” âŸ¶ Î“) (A : Î“ âŸ¶ M.Ty) (t : Î” âŸ¶ M.Tm)
    (tTp : t â‰« M.tp = Ïƒ â‰« A) :
    M.substCons Ïƒ A t tTp â‰« M.disp A = Ïƒ := by
  simp [substCons]

@[reassoc (attr := simp)]
theorem substCons_var {Î” Î“ : Ctx} (Ïƒ : Î” âŸ¶ Î“) (A : Î“ âŸ¶ M.Ty) (t : Î” âŸ¶ M.Tm)
    (aTp : t â‰« M.tp = Ïƒ â‰« A) :
    M.substCons Ïƒ A t aTp â‰« M.var A = t := by
  simp [substCons]

@[simp]
theorem comp_substCons {Î˜ Î” Î“ : Ctx} (Ï„ : Î˜ âŸ¶ Î”) (Ïƒ : Î” âŸ¶ Î“) (A : Î“ âŸ¶ M.Ty) (t : Î” âŸ¶ M.Tm)
    (aTp : t â‰« M.tp = Ïƒ â‰« A) :
    Ï„ â‰« M.substCons Ïƒ A t aTp = M.substCons (Ï„ â‰« Ïƒ) A (Ï„ â‰« t) (by simp [*]) := by
  apply (M.disp_pullback A).hom_ext
  Â· simp
  Â· simp

@[reassoc (attr := simp)]
theorem substCons_apply_comp_var {Î” Î“ : Ctx} (Ïƒ : Î” âŸ¶ Î“) (A : Î“ âŸ¶ M.Ty) (s : Î” âŸ¶ M.ext A)
    (s_tp : s â‰« M.disp A = Ïƒ) :
    M.substCons Ïƒ A (s â‰« M.var A) (by rw [Category.assoc, var_tp, â† Category.assoc, s_tp]) =
    s := by
  apply (disp_pullback ..).hom_ext <;> simp [s_tp]

/--
```
Î” âŠ¢ Ïƒ : Î“.A
------------
Î” âŠ¢ â†‘âˆ˜Ïƒ : Î“
```
-/
def substFst {Î” Î“ : Ctx} {A : Î“ âŸ¶ M.Ty} (Ïƒ : Î” âŸ¶ M.ext A) : Î” âŸ¶ Î“ :=
  Ïƒ â‰« M.disp A

/--
```
Î” âŠ¢ Ïƒ : Î“.A
-------------------
Î” âŠ¢ vâ‚€[Ïƒ] : A[â†‘âˆ˜Ïƒ]
```
-/
def substSnd {Î” Î“ : Ctx} {A : Î“ âŸ¶ M.Ty} (Ïƒ : Î” âŸ¶ M.ext A) : Î” âŸ¶ M.Tm :=
  Ïƒ â‰« M.var A

theorem substSnd_tp {Î” Î“ : Ctx} {A : Î“ âŸ¶ M.Ty} (Ïƒ : Î” âŸ¶ M.ext A) :
    M.substSnd Ïƒ â‰« M.tp = (M.substFst Ïƒ) â‰« A := by
  simp [substSnd, substFst]

/--
Weaken a substitution.
```
Î” âŠ¢ Ïƒ : Î“  Î“ âŠ¢ A type  A' = A[Ïƒ]
------------------------------------
Î”.A' âŠ¢ â†‘â‰«Ïƒ : Î“  Î”.A' âŠ¢ vâ‚€ : A[â†‘â‰«Ïƒ]
------------------------------------
Î”.A' âŠ¢ (â†‘â‰«Ïƒ).vâ‚€ : Î“.A
```
-/
def substWk {Î” Î“ : Ctx} (Ïƒ : Î” âŸ¶ Î“) (A : Î“ âŸ¶ M.Ty)
    (A' := Ïƒ â‰« A) (eq : Ïƒ â‰« A = A' := by rfl) : M.ext A' âŸ¶ M.ext A :=
  M.substCons (M.disp _ â‰« Ïƒ) A (M.var _) (by simp [eq])

@[reassoc (attr := simp)]
theorem substWk_disp {Î” Î“ : Ctx} (Ïƒ : Î” âŸ¶ Î“) (A : Î“ âŸ¶ M.Ty) (A' eq) :
    M.substWk Ïƒ A A' eq â‰« M.disp A = M.disp A' â‰« Ïƒ := by
  simp [substWk]

@[reassoc (attr := simp)]
theorem substWk_var {Î” Î“ : Ctx} (Ïƒ : Î” âŸ¶ Î“) (A : Î“ âŸ¶ M.Ty) (A' eq) :
    M.substWk Ïƒ A A' eq â‰« M.var A = M.var A' := by
  simp [substWk]

lemma var_comp {Î” Î“ : Ctx} (Ïƒ : Î” âŸ¶ Î“) (A : Î“ âŸ¶ M.Ty) : M.var (Ïƒ â‰« A) =
    M.substWk Ïƒ A â‰« M.var A := by
  simp

/-- `sec` is the section of `disp A` corresponding to `a`.

  ===== Î“ ------ a --------Â¬
 â€–      â†“ sec             V
 â€–   M.ext A -----------> M.Tm
 â€–      |                  |
 â€–      |                  |
 â€–    disp A              M.tp
 â€–      |                  |
 â€–      V                  V
  ===== Î“ ------ A -----> M.Ty -/
def sec {Î“ : Ctx} (A : Î“ âŸ¶ M.Ty) (a : Î“ âŸ¶ M.Tm) (a_tp : a â‰« M.tp = A) : Î“ âŸ¶ M.ext A :=
  M.substCons (ğŸ™ Î“) A a (by simp [a_tp])

@[reassoc (attr := simp)]
theorem sec_disp {Î“ : Ctx} (A : Î“ âŸ¶ M.Ty) (a : Î“ âŸ¶ M.Tm) (a_tp : a â‰« M.tp = A) :
    M.sec A a a_tp â‰« M.disp A = ğŸ™ _ := by
  simp [sec]

@[reassoc (attr := simp)]
theorem sec_var {Î“ : Ctx} (A : Î“ âŸ¶ M.Ty) (a : Î“ âŸ¶ M.Tm) (a_tp : a â‰« M.tp = A) :
    M.sec A a a_tp â‰« M.var A = a := by
  simp [sec]

@[reassoc]
theorem comp_sec {Î” Î“ : Ctx} (Ïƒ : Î” âŸ¶ Î“) (A : Î“ âŸ¶ M.Ty) (ÏƒA) (eq : Ïƒ â‰« A = ÏƒA)
    (a : Î“ âŸ¶ M.Tm) (a_tp : a â‰« M.tp = A) :
    Ïƒ â‰« M.sec A a a_tp = M.sec ÏƒA (Ïƒ â‰« a) (by simp [eq, a_tp]) â‰« M.substWk Ïƒ A _ eq := by
  apply (M.disp_pullback _).hom_ext <;>
    simp [sec, substWk]

@[reassoc (attr := simp)]
theorem sec_apply_comp_var {Î“ : Ctx} (A : Î“ âŸ¶ M.Ty)
    (s : Î“ âŸ¶ M.ext A) (s_tp : s â‰« M.disp A = ğŸ™ _) :
    M.sec A (s â‰« M.var A) (by rw [Category.assoc, var_tp, â† Category.assoc, s_tp]; simp) = s := by
  apply substCons_apply_comp_var _ _ _ _ s_tp

structure PolymorphicSigma (U0 U1 U2 : UnstructuredUniverse Ctx) where
  (Sig : âˆ€ {Î“} {A : Î“ âŸ¶ U0.Ty}, (U0.ext A âŸ¶ U1.Ty) â†’ (Î“ âŸ¶ U2.Ty))
  (Sig_comp : âˆ€ {Î“ Î”} (Ïƒ : Î” âŸ¶ Î“) (A : Î“ âŸ¶ U0.Ty) {ÏƒA} (eq) (B : U0.ext A âŸ¶ U1.Ty),
    Sig (U0.substWk Ïƒ A ÏƒA eq â‰« B) = Ïƒ â‰« Sig B)
  (pair : âˆ€ {Î“} {A : Î“ âŸ¶ U0.Ty} (B : U0.ext A âŸ¶ U1.Ty) (a : Î“ âŸ¶ U0.Tm)
    (a_tp : a â‰« U0.tp = A) (b : Î“ âŸ¶ U1.Tm), b â‰« U1.tp = U0.sec A a a_tp â‰« B â†’
    (Î“ âŸ¶ U2.Tm))
  (pair_comp : âˆ€ {Î“ Î”} (Ïƒ : Î” âŸ¶ Î“) {A : Î“ âŸ¶ U0.Ty} {ÏƒA} (eq) (B : U0.ext A âŸ¶ U1.Ty)
    (a : Î“ âŸ¶ U0.Tm) (a_tp : a â‰« U0.tp = A) (b : Î“ âŸ¶ U1.Tm)
    (b_tp : b â‰« U1.tp = U0.sec A a a_tp â‰« B),
    pair (U0.substWk Ïƒ A ÏƒA eq â‰« B) (Ïƒ â‰« a) (by cat_disch) (Ïƒ â‰« b)
      (by simp [b_tp, comp_sec_assoc, eq]) =
      Ïƒ â‰« pair B a a_tp b b_tp)
  (pair_tp : âˆ€ {Î“} {A : Î“ âŸ¶ U0.Ty} (B : U0.ext A âŸ¶ U1.Ty)
    (a : Î“ âŸ¶ U0.Tm) (a_tp : a â‰« U0.tp = A) (b : Î“ âŸ¶ U1.Tm)
    (b_tp : b â‰« U1.tp = U0.sec A a a_tp â‰« B),
      pair B a a_tp b b_tp â‰« U2.tp = Sig B)
  (fst : âˆ€ {Î“} {A : Î“ âŸ¶ U0.Ty} (B : U0.ext A âŸ¶ U1.Ty) (s : Î“ âŸ¶ U2.Tm),
    s â‰« U2.tp = Sig B â†’ (Î“ âŸ¶ U0.Tm))
  (fst_tp : âˆ€ {Î“} {A : Î“ âŸ¶ U0.Ty} (B : U0.ext A âŸ¶ U1.Ty) (s : Î“ âŸ¶ U2.Tm)
    (s_tp : s â‰« U2.tp = Sig B), fst B s s_tp â‰« U0.tp = A)
  (snd : âˆ€ {Î“} {A : Î“ âŸ¶ U0.Ty} (B : U0.ext A âŸ¶ U1.Ty) (s : Î“ âŸ¶ U2.Tm),
    s â‰« U2.tp = Sig B â†’ (Î“ âŸ¶ U1.Tm))
  (snd_tp : âˆ€ {Î“} {A : Î“ âŸ¶ U0.Ty} (B : U0.ext A âŸ¶ U1.Ty) (s : Î“ âŸ¶ U2.Tm)
    (s_tp : s â‰« U2.tp = Sig B), snd B s s_tp â‰« U1.tp = U0.sec A (fst B s s_tp) (fst_tp ..) â‰« B)
  (fst_pair : âˆ€ {Î“} {A : Î“ âŸ¶ U0.Ty} (B : U0.ext A âŸ¶ U1.Ty)
    (a : Î“ âŸ¶ U0.Tm) (a_tp : a â‰« U0.tp = A) (b : Î“ âŸ¶ U1.Tm)
    (b_tp : b â‰« U1.tp = U0.sec A a a_tp â‰« B), fst B (pair B a a_tp b b_tp) (pair_tp ..) = a)
  (snd_pair : âˆ€ {Î“} {A : Î“ âŸ¶ U0.Ty} (B : U0.ext A âŸ¶ U1.Ty)
    (a : Î“ âŸ¶ U0.Tm) (a_tp : a â‰« U0.tp = A) (b : Î“ âŸ¶ U1.Tm)
    (b_tp : b â‰« U1.tp = U0.sec A a a_tp â‰« B), snd B (pair B a a_tp b b_tp) (pair_tp ..) = b)
  (eta : âˆ€ {Î“} {A : Î“ âŸ¶ U0.Ty} (B : U0.ext A âŸ¶ U1.Ty) (s : Î“ âŸ¶ U2.Tm)
    (s_tp : s â‰« U2.tp = Sig B), pair B (fst B s s_tp) (fst_tp ..) (snd B s s_tp) (snd_tp ..) = s)

namespace PolymorphicSigma

variable {U0 U1 U2 : UnstructuredUniverse Ctx}

def mk' (Sig : âˆ€ {Î“} {A : Î“ âŸ¶ U0.Ty}, (U0.ext A âŸ¶ U1.Ty) â†’ (Î“ âŸ¶ U2.Ty))
    (Sig_comp : âˆ€ {Î“ Î”} (Ïƒ : Î” âŸ¶ Î“) (A : Î“ âŸ¶ U0.Ty) {ÏƒA} (eq) (B : U0.ext A âŸ¶ U1.Ty),
      Sig (U0.substWk Ïƒ A ÏƒA eq â‰« B) = Ïƒ â‰« Sig B)
    (assoc : âˆ€ {Î“} {A : Î“ âŸ¶ U0.Ty} (B : U0.ext A âŸ¶ U1.Ty), U1.ext B â‰… U2.ext (Sig B))
    (assoc_comp : âˆ€ {Î“ Î”} (Ïƒ : Î” âŸ¶ Î“) {A : Î“ âŸ¶ U0.Ty} {ÏƒA} (eq) (B : U0.ext A âŸ¶ U1.Ty),
     (assoc (substWk U0 Ïƒ A ÏƒA eq â‰« B)).hom â‰« substWk U2 Ïƒ _ _ (Sig_comp ..).symm =
     substWk _ (substWk _ Ïƒ _ _ eq) _ â‰« (assoc B).hom )
    (assoc_disp : âˆ€ {Î“} {A : Î“ âŸ¶ U0.Ty} (B : U0.ext A âŸ¶ U1.Ty),
      (assoc B).hom â‰« disp .. = disp .. â‰« disp ..) :
    PolymorphicSigma U0 U1 U2 where
  Sig := Sig
  Sig_comp := Sig_comp
  pair B a a_tp b b_tp := U1.substCons (U0.sec _ a a_tp) B b (by simp [b_tp]) â‰«
    (assoc B).hom â‰« var ..
  pair_comp Ïƒ A ÏƒA eq B a a_tp b b_tp := by
    have : Ïƒ â‰« U1.substCons (U0.sec A a a_tp) B b b_tp =
      U1.substCons (U0.sec (ÏƒA) (Ïƒ â‰« a) (by simp [eq, a_tp])) (substWk U0 Ïƒ A ÏƒA eq â‰« B)
      (Ïƒ â‰« b) (by simp [b_tp, comp_sec_assoc, eq]) â‰« substWk U1 (substWk U0 Ïƒ A ÏƒA eq) B := by
      apply (disp_pullback ..).hom_ext
      Â· simp
      Â· apply (disp_pullback ..).hom_ext
        Â· simp [substWk_disp_assoc]
        Â· simp [substWk_disp]
    slice_rhs 1 2 => rw [this]
    slice_rhs 2 3 => rw [â† assoc_comp]
    simp
  pair_tp B a a_tp b b_tp := by
    slice_lhs 3 4 => rw [var_tp]
    slice_lhs 2 3 => rw [assoc_disp]
    simp
  fst B s s_tp := U2.sec _ s s_tp â‰« (assoc _).inv â‰« disp .. â‰« var ..
  fst_tp B s s_tp := by
    slice_lhs 4 5 => rw [var_tp]
    slice_lhs 3 4 => rw [â† assoc_disp]
    simp
  snd B s s_tp := U2.sec _ s s_tp â‰« (assoc _).inv â‰« var ..
  snd_tp B s s_tp := by
    slice_lhs 3 4 => rw [var_tp]
    simp only [â† Category.assoc]
    congr 2
    apply (disp_pullback ..).hom_ext
    Â· simp
    Â· simp [â† assoc_disp]
  fst_pair B a a_tp b b_tp := by
    simp only [â† Category.assoc]
    rw [sec_apply_comp_var _ _ _ (by simp [assoc_disp])]
    simp
  snd_pair B a a_tp b b_tp := by
    simp only [â† Category.assoc]
    rw [sec_apply_comp_var _ _ _ (by simp [assoc_disp])]
    simp
  eta B s s_tp := by
    simp only [â† Category.assoc]
    rw! [sec_apply_comp_var _ _ _ (by simp [â† assoc_disp])]
    rw [U1.substCons_apply_comp_var _ _ _ (by simp)]
    simp

variable (S : PolymorphicSigma U0 U1 U2)

lemma fst_comp {Î“ Î”} (Ïƒ : Î” âŸ¶ Î“) {A : Î“ âŸ¶ U0.Ty} {ÏƒA} (eq) {B : U0.ext A âŸ¶ U1.Ty}
    (s : Î“ âŸ¶ U2.Tm) (s_tp : s â‰« U2.tp = S.Sig B) :
    S.fst (U0.substWk Ïƒ A ÏƒA eq â‰« B) (Ïƒ â‰« s) (by simp [s_tp, S.Sig_comp]) =
    Ïƒ â‰« S.fst B s s_tp := by
  rw! [(S.eta B s (by simp [s_tp])).symm, â† S.pair_comp, S.fst_pair, S.fst_pair]

lemma snd_comp {Î“ Î”} (Ïƒ : Î” âŸ¶ Î“) {A : Î“ âŸ¶ U0.Ty} {ÏƒA} (eq) {B : U0.ext A âŸ¶ U1.Ty}
    (s : Î“ âŸ¶ U2.Tm) (s_tp : s â‰« U2.tp = S.Sig B) :
    S.snd (U0.substWk Ïƒ A ÏƒA eq â‰« B) (Ïƒ â‰« s) (by simp [s_tp, S.Sig_comp]) =
    Ïƒ â‰« S.snd B s s_tp := by
  rw! [(S.eta B s (by simp [s_tp])).symm, â† S.pair_comp, S.snd_pair, S.snd_pair]

end PolymorphicSigma

structure PolymorphicPi (U0 U1 U2 : UnstructuredUniverse Ctx) where
  (Pi : âˆ€ {Î“} {A : Î“ âŸ¶ U0.Ty}, (U0.ext A âŸ¶ U1.Ty) â†’ (Î“ âŸ¶ U2.Ty))
  (Pi_comp : âˆ€ {Î“ Î”} (Ïƒ : Î” âŸ¶ Î“) (A : Î“ âŸ¶ U0.Ty) {ÏƒA} (eq) (B : U0.ext A âŸ¶ U1.Ty),
    Pi (U0.substWk Ïƒ A ÏƒA eq â‰« B) = Ïƒ â‰« Pi B)
  (lam : âˆ€ {Î“} {A : Î“ âŸ¶ U0.Ty} (B : U0.ext A âŸ¶ U1.Ty)
    (b : U0.ext A âŸ¶ U1.Tm), b â‰« U1.tp = B â†’ (Î“ âŸ¶ U2.Tm))
  (lam_comp : âˆ€ {Î“ Î”} (Ïƒ : Î” âŸ¶ Î“) {A : Î“ âŸ¶ U0.Ty} {ÏƒA} (eq) (B : U0.ext A âŸ¶ U1.Ty)
    (b : U0.ext A âŸ¶ U1.Tm) (b_tp : b â‰« U1.tp = B),
    lam (U0.substWk Ïƒ A ÏƒA eq â‰« B) (U0.substWk Ïƒ A ÏƒA eq â‰« b) (by cat_disch) =
      Ïƒ â‰« lam B b b_tp)
  (lam_tp : âˆ€ {Î“} {A : Î“ âŸ¶ U0.Ty} (B : U0.ext A âŸ¶ U1.Ty)
    (b : U0.ext A âŸ¶ U1.Tm) (b_tp : b â‰« U1.tp = B),
      lam B b b_tp â‰« U2.tp = Pi B)
  (unLam : âˆ€ {Î“} {A : Î“ âŸ¶ U0.Ty} (B : U0.ext A âŸ¶ U1.Ty) (f : Î“ âŸ¶ U2.Tm),
    f â‰« U2.tp = Pi B â†’ (U0.ext A âŸ¶ U1.Tm))
  (unLam_tp : âˆ€ {Î“} {A : Î“ âŸ¶ U0.Ty} (B : U0.ext A âŸ¶ U1.Ty) (f : Î“ âŸ¶ U2.Tm)
    (f_tp : f â‰« U2.tp = Pi B), unLam B f f_tp â‰« U1.tp = B)
  (unLam_lam : âˆ€ {Î“} {A : Î“ âŸ¶ U0.Ty} (B : U0.ext A âŸ¶ U1.Ty)
    (b : U0.ext A âŸ¶ U1.Tm) (b_tp : b â‰« U1.tp = B), unLam B (lam B b b_tp) (lam_tp ..) = b)
  (lam_unLam : âˆ€ {Î“} {A : Î“ âŸ¶ U0.Ty} (B : U0.ext A âŸ¶ U1.Ty) (f : Î“ âŸ¶ U2.Tm)
    (f_tp : f â‰« U2.tp = Pi B), lam B (unLam B f f_tp) (unLam_tp ..) = f)

namespace PolymorphicPi

variable {U0 U1 U2 : UnstructuredUniverse Ctx} (P : PolymorphicPi U0 U1 U2)

lemma unLam_comp {Î“ Î”} (Ïƒ : Î” âŸ¶ Î“) {A : Î“ âŸ¶ U0.Ty} {ÏƒA} (eq) {B : U0.ext A âŸ¶ U1.Ty}
    (f : Î“ âŸ¶ U2.Tm) (f_tp : f â‰« U2.tp = P.Pi B) :
    P.unLam (U0.substWk Ïƒ A ÏƒA eq â‰« B) (Ïƒ â‰« f) (by simp [f_tp, P.Pi_comp]) =
    U0.substWk Ïƒ A ÏƒA eq â‰« P.unLam B f f_tp := by
  rw [â† P.unLam_lam (U0.substWk Ïƒ A ÏƒA eq â‰« B) (U0.substWk Ïƒ A ÏƒA eq â‰« P.unLam B f f_tp)]
  rw! [P.lam_comp Ïƒ eq B, P.lam_unLam]

end PolymorphicPi

structure PolymorphicIdIntro (U0 U1 : UnstructuredUniverse Ctx) where
  (Id : âˆ€ {Î“} {A : Î“ âŸ¶ U0.Ty} (a0 a1 : Î“ âŸ¶ U0.Tm), (a0 â‰« U0.tp = A) â†’ a1 â‰« U0.tp = A â†’
    (Î“ âŸ¶ U1.Ty))
  (Id_comp : âˆ€ {Î“ Î”} (Ïƒ : Î” âŸ¶ Î“) {A : Î“ âŸ¶ U0.Ty} (a0 a1 : Î“ âŸ¶ U0.Tm)
    (a0_tp : a0 â‰« U0.tp = A) (a1_tp : a1 â‰« U0.tp = A),
    Id (A := Ïƒ â‰« A) (Ïƒ â‰« a0) (Ïƒ â‰« a1) (by cat_disch) (by cat_disch) = Ïƒ â‰« Id a0 a1 a0_tp a1_tp)
  (refl : âˆ€ {Î“} {A : Î“ âŸ¶ U0.Ty} (a : Î“ âŸ¶ U0.Tm), a â‰« U0.tp = A â†’ (Î“ âŸ¶ U1.Tm))
  (refl_comp : âˆ€ {Î“ Î”} (Ïƒ : Î” âŸ¶ Î“) {A : Î“ âŸ¶ U0.Ty} (a : Î“ âŸ¶ U0.Tm)
    (a_tp : a â‰« U0.tp = A), refl (Ïƒ â‰« a) (by cat_disch) = Ïƒ â‰« refl a a_tp)
  (refl_tp : âˆ€ {Î“} {A : Î“ âŸ¶ U0.Ty} (a : Î“ âŸ¶ U0.Tm) (a_tp : a â‰« U0.tp = A),
    refl a a_tp â‰« U1.tp = Id a a a_tp a_tp)

section

variable {U0 U1 : UnstructuredUniverse Ctx} (i : PolymorphicIdIntro U0 U1)

namespace PolymorphicIdIntro

variable {Î“ Î”} (Ïƒ : Î” âŸ¶ Î“) {A : Î“ âŸ¶ U0.Ty} (a : Î“ âŸ¶ U0.Tm) (a_tp : a â‰« U0.tp = A)

@[simp]
lemma refl_tp' : i.refl a a_tp â‰« U1.tp = i.Id a a a_tp a_tp := refl_tp ..

/-- Given `Î“ âŠ¢ a : A` this is the identity type weakened to the context
`Î“.(x : A) âŠ¢ Id(a,x) : U1.Ty` -/
@[simp]
abbrev weakenId : U0.ext A âŸ¶ U1.Ty :=
  i.Id (A := U0.disp A â‰« A) (U0.disp A â‰« a) (U0.var A) (by cat_disch) (by cat_disch)

lemma weakenId_comp : i.weakenId (A := Ïƒ â‰« A) (Ïƒ â‰« a) (by simp [a_tp]) =
    U0.substWk Ïƒ A â‰« i.weakenId a a_tp := by
  simp [â† Id_comp]

/-- Given `Î“ âŠ¢ a : A` this is the context `Î“.(x : A).(h:Id(a,x))` -/
@[simp]
abbrev motiveCtx : Ctx :=
  U1.ext (i.weakenId a a_tp)

/-- Given `Î“ âŠ¢ a : A`, `reflSubst` is the substitution `(a,refl) : Î“ âŸ¶ Î“.(x:A).(h:Id(a,x))`
appearing in identity elimination `J`  so that we can write `Î“ âŠ¢ r : C(a,refl)` -/
abbrev reflSubst : Î“ âŸ¶ i.motiveCtx a a_tp :=
  U1.substCons (U0.sec A a a_tp) (i.weakenId a a_tp) (i.refl a a_tp) (by simp [â† Id_comp])

/-- Given a substitution `Ïƒ : Î” âŸ¶ Î“` and `Î“ âŠ¢ a : A`,
this is the substitution `Î”.(x: Ïƒ â‰« A).(h:Id(Ïƒ â‰« a,x)) âŸ¶ Î“.(x:A).(h:Id(a,x))`-/
abbrev motiveSubst {ÏƒA} (eq : ÏƒA = Ïƒ â‰« A := by rfl) :
    i.motiveCtx (A := ÏƒA) (Ïƒ â‰« a) (by cat_disch) âŸ¶ i.motiveCtx a a_tp :=
  substWk _ (substWk _ Ïƒ _ _ (by simp [eq])) _ _ (by
    simp [â† Id_comp, substWk_disp_assoc, eq])

@[reassoc (attr := simp)]
lemma reflSubst_comp_motiveSubst {ÏƒA} (eq : ÏƒA = Ïƒ â‰« A := by rfl) :
    i.reflSubst (Ïƒ â‰« a) (by cat_disch) â‰« i.motiveSubst Ïƒ a a_tp eq =
    Ïƒ â‰« i.reflSubst a a_tp := by
  subst a_tp eq
  repeat any_goals apply (disp_pullback ..).hom_ext
  any_goals simp [â† refl_comp, substWk_disp]

end PolymorphicIdIntro

structure PolymorphicIdElim (U2 : UnstructuredUniverse Ctx) where
  (j : âˆ€ {Î“} {A : Î“ âŸ¶ U0.Ty} (a : Î“ âŸ¶ U0.Tm) (a_tp : a â‰« U0.tp = A)
    (C : i.motiveCtx a a_tp âŸ¶ U2.Ty) (c : Î“ âŸ¶ U2.Tm),
    (c â‰« U2.tp = (i.reflSubst a a_tp) â‰« C) â†’ (i.motiveCtx a a_tp âŸ¶ U2.Tm))
  (comp_j : âˆ€ {Î“ Î”} (Ïƒ : Î” âŸ¶ Î“) {A : Î“ âŸ¶ U0.Ty} (a : Î“ âŸ¶ U0.Tm)
    (a_tp : a â‰« U0.tp = A) (C : i.motiveCtx a a_tp âŸ¶ U2.Ty) (c : Î“ âŸ¶ U2.Tm)
    (c_tp : c â‰« U2.tp = (i.reflSubst a a_tp) â‰« C),
    j (Ïƒ â‰« a) (by cat_disch) (i.motiveSubst Ïƒ a a_tp rfl â‰« C) (Ïƒ â‰« c) (by cat_disch) =
    i.motiveSubst Ïƒ a a_tp â‰« j a a_tp C c c_tp)
  (j_tp : âˆ€ {Î“} {A : Î“ âŸ¶ U0.Ty} (a : Î“ âŸ¶ U0.Tm) (a_tp : a â‰« U0.tp = A)
    (C : i.motiveCtx a a_tp âŸ¶ U2.Ty) (c : Î“ âŸ¶ U2.Tm)
    (c_tp : c â‰« U2.tp = (i.reflSubst a a_tp) â‰« C),
    j a a_tp C c c_tp â‰« U2.tp = C)
  (reflSubst_j : âˆ€ {Î“} {A : Î“ âŸ¶ U0.Ty} (a : Î“ âŸ¶ U0.Tm) (a_tp : a â‰« U0.tp = A)
    (C : i.motiveCtx a a_tp âŸ¶ U2.Ty) (c : Î“ âŸ¶ U2.Tm)
    (c_tp : c â‰« U2.tp = (i.reflSubst a a_tp) â‰« C),
    i.reflSubst a a_tp â‰« j a a_tp C c c_tp = c)

end

end UnstructuredUniverse

end Model
